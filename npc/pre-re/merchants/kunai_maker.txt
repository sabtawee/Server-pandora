//===== rAthena Script =======================================
//= Kunai Merchant Kashin
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.3a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Trades a few shurikens + ninja stones for elemental kunai.
//===== Additional Comments: =================================
//= 1.0 Added the npc. It uses a function that sends the item 
//=     id of the 2 required items plus the amount. Can trade
//=     up to 500 units (5,000 kunais) at once. [erKURITA]
//= 1.1 Officialized script [Playtester]
//= 1.2a Optimized/cleaned up a bit [ultramage]
//= 1.3 Updated to match AEGIS script. [Kisuka]
//= 1.3a Kagerou/Oboro support (BaseJob -> BaseClass) [Euphy]
//============================================================

izlude,135,101,4	script	Kunai Merchant Kashin	83,{
	if(BaseClass == Job_Ninja) {
		mes "[Kashin]";
		mes "ฉันคือ Kashin ผู้จัดจำหน่าย";
		mes "ของ Kunai สำหรับนินจาเอามา";
		mes "มองไปรอบ ๆ และให้ฉัน";
		mes "รู้ว่าคุณสนใจหรือไม่";
		mes "ในสินค้าใด ๆ ของฉัน";
		next;
		switch(select("10 Fell Poison Kunai:10 Icicle Kunai:10 High Wind Kunai:10 Black Earth Kunai:10 Heat Wave Kunai:Cancel")) {
		case 1: callfunc "Kunai_Trade",13250,20,7524,1,13259; break;
		case 2: callfunc "Kunai_Trade",13251,8,7522,2,13255; break;
		case 3: callfunc "Kunai_Trade",13252,4,7523,2,13257; break;
		case 4: callfunc "Kunai_Trade",13253,2,7524,1,13256; break;
		case 5: callfunc "Kunai_Trade",13254,1,7521,2,13258; break;
		case 6:
			mes "[Kashin]";
			mes "แล้วขอบคุณ";
			mes "สำหรับการเยี่ยมชมร้านค้าของฉัน";
			mes "โปรดมาหาฉันเมื่อ";
			mes "คุณต้องซื้อบางอย่าง";
			mes "Kunaisลาก่อนตอนนี้ ~";
			close;
		}
	}
	mes "[Kashin]";
	mes "ฉันคือ Kashin ผู้จัดจำหน่าย";
	mes "ของ Kunai สำหรับนินจาถ้าคุณ";
	mes "มีเพื่อนที่เป็น";
	mes "นินจาจากนั้นคุณอาจ";
	mes "ต้องการบอกพวกเขาเกี่ยวกับฉัน";
	close;
Oninit:
	waitingroom " [ Kunai Shop ] ",0;
	end;
}

function	script	Kunai_Trade	{
	setarray .@item_req[0], getarg(0), getarg(2);
	setarray .@req_amount[0], getarg(1), getarg(3);
	.@item_id = getarg(4);

	mes "[Kashin]";
	mes "คุณสามารถแลกเปลี่ยนได้";
	mes .@req_amount[0] +" "+ getitemname(.@item_req[0]) +" และ";
	mes .@req_amount[1] +" "+ getitemname(.@item_req[1]) +" สำหรับทุก ๆ";
	mes "ชุดของ 10 "+ getitemname(.@item_id) +".";
	next;
	mes "[Kashin]";
	mes "ฉันสามารถให้ Kunais ได้สูงสุด 500 ชุดเท่านั้น";
	mes "หากคุณต้องการยกเลิกการค้าโปรดป้อน '' 0. '' เท่าไหร่";
	mes "คุณต้องการชุด kunai ไหม";
	next;
	input .@amount;
	if(.@amount < 1 || .@amount > 500) {
		mes "[Kashin]";
		mes "เอ๊ะ?ฉันขอโทษ แต่";
		mes "ฉันไม่สามารถให้คุณได้";
		mes "Kunai หลายชุดโปรด";
		mes "ป้อนค่าน้อยกว่า 500";
		close;
	}
	if (countitem(.@item_req[0]) < .@amount*.@req_amount[0] || countitem(.@item_req[1]) < .@amount*.@req_amount[1]) {
		mes "[Kashin]";
		mes "อืมคุณไม่มี";
		mes "รายการเพียงพอสำหรับสิ่งนี้";
		mes "Kunai Exchangeโปรด";
		mes "ตรวจสอบรายการของคุณอีกครั้ง";
		close;
 	}
	if (checkweight(.@item_id, .@amount*10) == 0) {
		mes "[Kashin]";
		mes "อืมดูเหมือนว่า";
		mes "สินค้าคงคลังไม่มี";
		mes "มีพื้นที่เพียงพอในการจัดเก็บ";
		mes "รายการเพิ่มเติมคุณดีกว่า";
		mes "เพิ่มพื้นที่ว่างก่อน";
		close;
	}
	mes "[Kashin]";
	mes "เยี่ยมมากทุกอย่างอยู่ใน";
	mes "สั่งซื้อกันเถอะ";
	mes "และทำการค้านี้ให้เสร็จสมบูรณ์";
	mes "ฉันแน่ใจว่าคุณจะเป็น";
	mes "ค่อนข้างพอใจกับ";
	mes "ธีมเป็นทวารหนัก";
	delitem .@item_req[0],.@req_amount[0]*.@amount;
	delitem .@item_req[1],.@req_amount[1]*.@amount;
	getitem .@item_id,10*.@amount;
	close;
}
