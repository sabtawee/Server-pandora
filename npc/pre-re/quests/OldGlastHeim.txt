//===== rAthena Script =======================================
//= Old Glast Heim
//===== Description: =========================================
//= Discover the history of events that took place in the
//= Glast Heim castle and how it ended up in ruins.
//===== Changelogs: =================================
//= 1.0 First version. [Euphy, Ziu, Heris]
//=     This is a custom version, and may contain bugs.
//= 1.1 Bug fixes; removed redundant OnInstanceInit scripts.
//= 1.2 Add NPC Hugin's Follower [exneval]
//=     NPC that give access to Glast Heim Nightmare Mode.
//= 1.3 Add some NPCs placeholder. [exneval]
//= 1.4 Update to its official text. [exneval]
//=     Support merchant, enchant, socket features.
//= 1.5 idAthena merge. Removed Hugin's Follower NPCs. [Secretdataz]
//=     Changed `set` calls to direct assignments.
//=     Cleaned up the script.
//=     TODO: Confirm Hugin NPC's code flow.
//= 1.6 Cleaned up the dialogue. [Aleos]
//============================================================

glast_01,204,273,5	script	Hugin	4_M_SAGE_C,{	
	if (BaseLevel < 80) {
		mes "[Hugin]";
		mes "ทำไมคุณไม่กลับมาหลังจาก level 80 ล่ะ?";
		close;
	}
	if (isbegin_quest(12316) == 0) {
		mes "[Hugin]";
		mes "นานมาแล้ว ปราสาทแห่งนี้ไม่ได้มีลักษณะเช่นนี้";
		next;
		mes "[Hugin]";
		mes "อา, ข้าขอโทษ";
		next;
		mes "[Hugin]";
		mes "ข้าชื่อฮักอิน ข้ากำลังศึกษาช่องว่างมิติระหว่างเวลาและอวกาศ";
		next;
		select("ที่นี่มีอะไรแบบนั้นเหรอ?");
		mes "[Hugin]";
		mes "คุณเคยสงสัยเกี่ยวกับประวัติของ Glast Heim หรือไม่?";
		next;
		mes "[Hugin]";
		mes "มีกษัตริย์ชื่อชูมิเช ซึ่งเป็นที่รู้จักในฐานะทรราช ในประวัติศาสตร์อาณาจักรรูนมิดการ์ท";
		next;
		mes "[Hugin]";
		mes "เวลาจะเปิดเผยคำตอบที่แท้จริงหากเรามองไปในสถานที่ที่เหมาะสม";
		next;
		if (select("ข้าไม่สนใจประวัติศาสตร์ "," นั่นก็น่าสนใจ เจออะไรรึเปล่า?") == 1) {
			mes "[Hugin]";
			mes "อืม, โปรดกลับมาใหม่ในภายหลังเมื่อคุณสนใจแล้ว";
			close;
		}
		mes "[Hugin]";
		mes "อันที่จริง การเดินทางข้ามเวลาเป็นไปได้ แต่ข้ากลัวเกินไปที่จะไปที่นั่น";
		next;
		mes "[Hugin]";
		mes "อาจเป็นไปได้สำหรับคุณ!";
		next;
		mes "[Hugin]";
		mes "คุณต้องการเดินทางข้ามเวลาหรือไม่?";
		next;
		if (select("ไม่ล่ะ ขอบคุณ "," ลุยเลย!") == 1) {
			mes "[Hugin]";
			mes "การกระโดดกิกะวัตต์นี่เป็นโอกาสที่ดีมาก";
			close;
		}
		mes "[Hugin]";
		mes "ข้าหวังว่าท่านจะเข้าใจสิ่งที่ข้าพูด ช่วยบอกข้าทีว่าท่านต้องการทำอะไร?";
		setquest 12316;// พบกับ Hugin
		completequest 12316;
	}
	else {
		mes "[Hugin]";
		mes "อืม? เราเคยพบกันมาก่อนไหม?";
		if (isbegin_quest(12322) == 1)
			erasequest 12322;
	}
	next;
	switch( checkquest(12317,PLAYTIME) ) {
	case -1:
		.@party_id = getcharid(1);
		.@p_name$ = getpartyname(.@party_id);
		.@md_name$ = "Old Glast Heim";

		if (!instance_check_party(.@party_id)) {
			mes "[Hugin]";
			mes "คุณไม่มีปาร์ตี้!!";
			close;
		}
		if (is_party_leader() == true)
			.@menu$ = "สร้างดันเจี้ยน Old Glast Heim";
		else {
			mes "[Hugin]";
			mes "เราเคยพบกันมาก่อนหรือไม่? ไม่มีทาง. นี่เป็นครั้งแรกที่ข้าได้พบกับท่าน ท่านต้องการอะไร?";
		}
		if (isbegin_quest(12318) == 0)
			setquest 12318;// Corrupted Soul Hunt
		switch( select( .@menu$, "เข้าสู่ดันเจี้ยน", "ยกเลิก" ) ) {
		case 1:
			switch( instance_create(.@md_name$) ) {
			case -3:
				dispbottom "Memorial Dungeon, 'Old Glast Heim' is already in progress.",0xFFFFFF;
				close;
			case -4:
			case -2:
			case -1:
				mes "Party Name: " + getpartyname( getcharid(1) );
				mes "Party Leader: " + strcharinfo(0);
				mes "^0000ff" + .@md_name$ + "^000000 - time gap generation failed.";
				close;
			}
			mes "[Hugin]";
			mes "หลังจากช่องว่างมิติเวลาเปิดขึ้นโปรดแจ้งให้ข้าทราบอีกครั้ง";
			close;
		case 2:
			switch( instance_enter(.@md_name$) ) {
			case IE_OTHER:
				mes "[Hugin]";
				mes "ข้อผิดพลาดที่ไม่รู้จักได้เกิดขึ้น.";
				close;
			case IE_NOINSTANCE:
				mes "[Hugin]";
				mes "ช่องว่างมิติเวลายังไม่เปิด";
				close;
			case IE_NOMEMBER:
				mes "[Hugin]";
				mes "คุณจะไม่สามารถเข้าได้ ถ้าคุณไม่ได้อยู่ในปาร์ตี้";
				close;
			case IE_OK:
				mapannounce "glast_01", "" + getpartyname( getcharid(1) ) + " party member " + strcharinfo(0) + " enters the " + .@md_name$ + ".", bc_map,0x00FF99;
				setquest 12317;// Trace of Time Travel
				// warp "1@gl_k",150,20;
				end;
			}
		case 3:
			close;
		}
	case 0:
	case 1:
		mes "[Hugin]";
		mes "โอ้พระเจ้า...";
		mes "คุณไม่สามารถเดินทางได้อีก";
		next;
		mes "[Hugin]";
		mes "โปรดหยุดพักและกลับมาอีกครั้งในภายหลัง";
		close;
	case 2:
		mes "^0000ffร่องรอยการเข้าถึง Old Glast Heim ทั้งหมดถูกลบออก ตอนนี้คุณสามารถคุยกับ Hugin ได้อีกครั้ง^000000";
		if (isbegin_quest(12318) == 0)
			setquest 12318;// Corrupted Soul Hunt
		erasequest 12317;
		close;
	}
}

// Warps
1@gl_k,96,80,0	warp	#2Control	2,2,1@gl_k,80,80
1@gl_k,90,80,0	warp	#2Control2	2,2,1@gl_k,105,80
1@gl_k,202,79,0	warp	#3Control	2,2,1@gl_k,215,79
1@gl_k,206,79,0	warp	#3Control2	2,2,1@gl_k,195,79
1@gl_k,227,216,0	warp	#4Control	2,2,1@gl_k,215,216
1@gl_k,222,216,0	warp	#4Control2	2,2,1@gl_k,233,216
1@gl_k,150,284,0	warp	#2F Entrance	2,2,2@gl_k,150,46
2@gl_k,149,32,0	warp	#1 Control	2,2,1@gl_k,150,270
2@gl_k,145,123,0	warp	#22 Control	2,2,2@gl_k,126,123
2@gl_k,136,122,0	warp	#22 Control2	2,2,2@gl_k,150,116
2@gl_k,154,101,0	warp	#23 Control	2,2,2@gl_k,174,101
2@gl_k,165,101,0	warp	#23 Control2	2,2,2@gl_k,150,111
2@gl_k,150,163,0	warp	#24 Control	2,2,2@gl_k,150,179
2@gl_k,150,167,0	warp	#24 Control2	2,2,2@gl_k,150,149
1@gl_k,69,168,0	warp	#Secret Room Exit	2,2,1@gl_k,48,168

// Floor 1
//============================================================
1@gl_k,149,41,5	script	Varmundt#0	4_M_BARMUND,{
	if (is_party_leader() == false) {
		mes "[Varmundt]";
		mes "หัวหน้าของคุณอยู่ที่ไหน ข้าต้องการความช่วยเหลือจากเขา";
		cutin "gl_barmund2",2;
		close3;
	}
	.@account_id = getcharid(3);
	.@player_name$ = strcharinfo(0);
	mes "[Varmundt]";
	mes "ท่านเป็นคนที่ Herico ส่งมาเพื่อช่วยข้าหรือไม่?";
	cutin "gl_barmund1",2;
	next;
	select("โอ้. ข้า ...");
	mes "[" + .@player_name$ + "]";
	mes "ใช่ ข้าเป็นคนที่ Herico บอกให้ข้ามาพบท่าน";
	next;
	mes "[Varmundt]";
	mes "เรามีเวลาไม่มาก เราต้องรายงานเซอร์ไฮน์ริชเกี่ยวกับการรุกรานของฮิเมลเมซ";
	next;
	select("Himelmez...");
	mes "[" + .@player_name$ + "]";
	mes "ฮิเมลเมซคือใคร?";
	next;
	mes "[Varmundt]";
	mes "Herico ไม่ได้บอกท่าน?";
	next;
	mes "[Varmundt]";
	mes "เธอคือผู้ปกครองแห่งความตาย Dead man's Valkyrie. ฮิเมลเมซต้องการที่จะเอาหัวใจของ Ymir ไปจากเรา";
	next;
	mes "[Varmundt]";
	mes "เธออาจทำลายปราสาททั้งหลังได้ถ้าเธอต้องการ";
	next;
	mes "[Varmundt]";
	mes "เร็วเข้า! รายงานต่อท่าน. ไฮน์ริชเกี่ยวกับฮิเมลเมซ ข้าต้องตรวจสอบด่านป้องกันที่นี่!";
	close2;
	cutin "",255;
	disablenpc instance_npcname("Varmundt#0");
	enablenpc instance_npcname("Heinrich#1");
	enablenpc instance_npcname("Varmundt#1");
	end;
}

1@gl_k,145,104,6	script	Khalitzburg Knightage#1	4_F_KHALITZBURG,{
	mes "[Khalitzburg Knightage]";
	mes "กะจะเลิกงานเร็ว ๆ นี้ แต่ลูกเรือคนต่อไปไม่มา";
	close;
}

1@gl_k,154,104,3	script	Khalitzburg Knightage#2	4_F_KHALITZBURG,{
	mes "[Khalitzburg Knightage]";
	mes "มีอะไรอยากบอกข้าไหม?";
	close;
}

1@gl_k,145,99,6	script	White Knight#3	4_WHITEKNIGHT,{
	mes "[White Knight]";
	mes "ท่านอยู่กับ Varmundt หรือไม่? ผู้บัญชาการกำลังรอท่านอยู่";
	close;
}

1@gl_k,154,99,3	duplicate(Khalitzburg Knightage#1)	White Knight#4	4_WHITEKNIGHT

1@gl_k,145,94,6	script	Khalitzburg Knightage#5	4_F_KHALITZBURG,{
	mes "[Khalitzburg Knightage]";
	mes "เมื่อคืนฉันฝันแปลก ๆ แม่ของฉันอยู่ในนั้น ... สงสัยว่าเธอจะโอเคไหม ...";
	close;
}

1@gl_k,154,94,3	duplicate(White Knight#3)	Khalitzburg Knightage#6	4_F_KHALITZBURG

1@gl_k,145,89,6	script	White Knight#7	4_WHITEKNIGHT,{
	mes "[White Knight]";
	mes "กรุณาอย่าทำเรื่องวุ่นวายที่นี่";
	close;
}
1@gl_k,154,89,3	duplicate(White Knight#7)	White Knight#8	4_WHITEKNIGHT

1@gl_k,145,84,6	script	Khalitzburg Knightage#9	4_F_KHALITZBURG,{
	mes "[Khalitzburg Knightage]";
	mes "Orders please.";
	close;
}
1@gl_k,154,84,3	duplicate(Khalitzburg Knightage#9)	Khalitzburg Knightage#10	4_F_KHALITZBURG

1@gl_k,145,79,6	script	White Knight#11	4_WHITEKNIGHT,{
	mes "[White Knight]";
	mes "ท่านรู้จัก Varmundt ได้อย่างไร? ข้าได้ยินมาว่าเขาไม่ใช่ผู้ชายที่เป็นมิตรจริงๆ ";
	close;
}
1@gl_k,154,79,3	duplicate(White Knight#11)	White Knight#12	4_WHITEKNIGHT

1@gl_k,145,74,6	script	Khalitzburg Knightage#13	4_F_KHALITZBURG,{
	mes "[Khalitzburg Knightage]";
	mes "ปราสาทแห่งนี้ดูแปลกไปในทันทีไม่ใช่หรือ? มีบางอย่างไม่ถูกต้อง";
	close;
}
1@gl_k,154,74,3	duplicate(Khalitzburg Knightage#13)	Khalitzburg Knightage#14	4_F_KHALITZBURG

1@gl_k,145,69,6	script	White Knight#15	4_WHITEKNIGHT,{
	mes "[White Knight]";
	mes "ข้าไม่ชอบคุยระหว่างทำงาน";
	close;
}
1@gl_k,154,69,3	duplicate(White Knight#15)	White Knight#16	4_WHITEKNIGHT

1@gl_k,145,64,6	script	Khalitzburg Knightage#17	4_F_KHALITZBURG,{
	mes "[Khalitzburg Knightage]";
	mes "...";
	close;
}
1@gl_k,154,64,3	duplicate(Khalitzburg Knightage#17)	Khalitzburg Knightage#18	4_F_KHALITZBURG

1@gl_k,145,59,6	script	White Knight#19	4_WHITEKNIGHT,{
	mes "[White Knight]";
	mes "ข้าจะทำอะไรให้ท่านได้บ้าง?";
	close;
}
1@gl_k,154,59,3	duplicate(White Knight#19)	White Knight#20	4_WHITEKNIGHT

1@gl_k,145,54,6	script	Khalitzburg Knightage#21	4_F_KHALITZBURG,{
	mes "[Khalitzburg Knightage]";
	mes "มีอะไรที่ท่านต้องการหรือไม่?";
	close;
}
1@gl_k,154,54,3	duplicate(Khalitzburg Knightage#21)	Khalitzburg Knightage#22	4_F_KHALITZBURG

1@gl_k,149,100,6	script	Heinrich#1	4_M_HEINRICH,{
	if (is_party_leader() == false) {
		mes "[Heinrich]";
		mes "หัวหน้าของท่านอยู่ที่ไหน? ข้าต้องคุยกับเขา";
		cutin "gl_heinrich2",2;
		close3;
	}
	.@account_id = getcharid(3);
	.@player_name$ = strcharinfo(0);
	cutin "gl_heinrich2",2;
	select("Sir Heinrich. Varmundt...");
	mes "[" + .@player_name$ + "]";
	mes "เซอร์ไฮน์ริช ท่านรู้หรือไม่ว่าตอนนี้เกิดอะไรขึ้นในปราสาท?";
	next;
	mes "[Heinrich]";
	mes "ท่านไม่ใช่นักผจญภัยที่มาพร้อมกับ Varmundt หรือ?";
	next;
	mes "[Heinrich]";
	mes "มันคืออะไร? มีอะไรผิดปกติกับปราสาท?";
	next;
	select("การบุกของฮิเมลเมซ ...");
	mes "[" + .@player_name$ + "]";
	mes "Dead man's Valkyrie, ฮิเมลเมซกำลังจะนำชิ้นส่วนหัวใจของ Ymir ที่ซ่อนอยู่ในปราสาทแห่งนี้!";
	next;
	mes "[Heinrich]";
	mes "ฮ่า ๆ . ตลก. ท่านคิดว่าเป็นไปได้จริงๆหรือ?";
	next;
	mes "[Varmundt]";
	mes "ข้าไม่ได้ล้อเล่นเซอร์ไฮน์ริช ถ้าข้าถูกต้องเธอจะมาที่นี่ในไม่ช้า";
	next;
	mes "[Varmundt]";
	mes "เราต้องซ่อนชิ้นส่วนหัวใจไว้ในที่ปลอดภัยก่อนที่การโจมตีของฮิเมลเมซจะเริ่มขึ้น!";
	next;
	select("แม้ว่าท่านจะไม่เชื่อข้าก็ตาม ...");
	mes "[" + .@player_name$ + "]";
	mes "ข้าไม่สามารถทำให้ท่านเชื่อข้าได้ แต่ไม่มีเวลาเถียง!";
	cutin "",255;
	next;
	mes "[Heinrich]";
	mes "ขอขอบคุณสำหรับความช่วยเหลือ. แต่ตอนนี้เราไม่มีกษัตริย์อยู่กับเราด้วยซ้ำ";
	cutin "gl_heinrich1",2;
	next;
	mes "[Heinrich]";
	mes "เราไม่สามารถเสี่ยงที่จะขยับหัวใจได้เพียงเพราะคนแปลกหน้าบางคนพูดเช่นนั้น";
	close2;
	disablenpc instance_npcname("Heinrich#1");
	enablenpc instance_npcname("Heinrich#2");
	enablenpc instance_npcname("Himelmez#1");
	donpcevent instance_npcname("#talkinstance1") + "::OnEnable";
	cutin "",255;
	end;
}

1@gl_k,152,97,3	script	Varmundt#1	4_M_BARMUND,{ end; }
1@gl_k,149,97,5	duplicate(Varmundt#1)	Heinrich#2	4_M_HEINRICH
1@gl_k,149,100,5	duplicate(Varmundt#1)	Heinrich#3	4_M_HEINRICH
1@gl_k,149,89,1	duplicate(Varmundt#1)	Himelmez#1	4_F_HIMEL

// Control Timer
//============================================================
1@gl_k,1,1,0	script	#talkinstance1	HIDDEN_WARP_NPC,{
	end;
OnEnable:
	mapannounce 'map_name$[0], "????'s: ฮ่า ฮ่า ฮ่า ฮ่า~!", bc_map,0xFFFF00,FW_NORMAL,18;
	initnpctimer;
	'npc_himelmez1$ = instance_npcname("Himelmez#1");
	'npc_varmundt1$ = instance_npcname("Varmundt#1");
	'npc_heinrich2$ = instance_npcname("Heinrich#2");
	'npc_heinrich3$ = instance_npcname("Heinrich#3");
	end;
OnTimer1500:
	npctalk "Heinrich: เจ้าคือใคร?", 'npc_heinrich2$;
	end;
OnTimer4500:
	npctalk "Himelmez: อืม ~ ข้าขัดจังหวะท่านหรือไม่? ท่านไม่คาดหวังข้าเหรอ?", 'npc_himelmez1$;
	end;
OnTimer10000:
	npctalk "Varmundt: Himelmez!! มาแล้ว!", 'npc_varmundt1$;
	end;
OnTimer17500:
	npctalk "Himelmez: ข้าชื่อ is Lisa Kahn Himelmez. Master of Dullahan, Dead man's Valkyrie, นั่นคือสิ่งที่พวกเขาเรียกข้า", 'npc_himelmez1$;
	end;
OnTimer22000:
	npctalk "Heinrich: ข้าไม่กลัวเจ้า ... ", 'npc_heinrich2$;
	end;
OnTimer28500:
	npctalk "Himelmez: มาดูกันว่าเจ้าจะผ่อนคลายแบบนั้นได้ไหม หลังจากพบว่าราชาของเจ้าอยู่ที่ไหน", 'npc_himelmez1$;
	end;
OnTimer36000:
	npctalk "Heinrich: อะไร?", 'npc_heinrich2$;
	end;
OnTimer41000:
	npctalk "Himelmez: อืม ตอนนี้ข้าสนใจเจ้าแล้วใช่ไหม", 'npc_himelmez1$;
	end;
OnTimer47000:
	npctalk "Himelmez: เจ้าไม่ต้องการตำแหน่งกษัตริย์เพียงแค่รอให้เขากลับมา ข้าชอบความบริสุทธิ์ของท่าน", 'npc_himelmez1$;
	end;
OnTimer54000:
	npctalk "Himelmez: เป็นเรื่องน่าเสียดายที่ได้พบเจ้าในสถานการณ์เช่นนี้", 'npc_himelmez1$;
	end;
OnTimer59000:
	npctalk "Heinrich: กษัตริย์ของข้าไปเยี่ยมราชวงศ์รูนมิดการ์ท เขายังไม่กลับมา", 'npc_heinrich2$;
	end;
OnTimer66500:
	npctalk "Heinrich: และตอนนี้เจ้ากำลังพยายามหลอกข้า ว่าเกิดอะไรขึ้นกับเขา!", 'npc_heinrich2$;
	end;
OnTimer71500:
	npctalk "Himelmez: อืม ~ ข้าชอบที่จะนั่งลงและอธิบายให้เจ้าฟัง แต่วันนี้ข้ายุ่งมาก ~", 'npc_himelmez1$;
	end;
OnTimer78000:
	npctalk "Himelmez: ข้ามีธุระต้องดูแล ลูกน้องของข้าจะปฏิบัติกับเจ้าอย่างดี~", 'npc_himelmez1$;
	end;
OnTimer84500:
	npctalk "Heinrich: เธอคงรู้แล้วว่าชิ้นส่วนหัวใจของ Ymir อยู่ที่ไหน", 'npc_heinrich2$;
	disablenpc 'npc_himelmez1$;
	end;
OnTimer90500:
	npctalk "Heinrich: Khalitzburg และ White Knights ทุกคนควรติดตามข้าตอนนี้ ...", 'npc_heinrich2$;
	end;
OnTimer92000:
	for (.@i = 1; .@i <= 20; .@i += 4) {
		hideonnpc instance_npcname("Khalitzburg Knightage#" + .@i);
		hideonnpc instance_npcname("Khalitzburg Knightage#" + (.@i+1));
		hideonnpc instance_npcname("White Knight#" + (.@i+2));
		hideonnpc instance_npcname("White Knight#" + (.@i+3));
	}
	hideonnpc instance_npcname("Khalitzburg Knightage#21");
	hideonnpc instance_npcname("Khalitzburg Knightage#22");

	.@label$ = instance_npcname("#talkinstance1") + "::OnMyMobDead";
	monster 'map_name$[0],145,59,"Abysmal Knight",2470,1,.@label$;// MG_KNIGHT_OF_ABYSS
	monster 'map_name$[0],154,59,"Abysmal Knight",2470,1,.@label$;
	monster 'map_name$[0],145,69,"Abysmal Knight",2470,1,.@label$;
	monster 'map_name$[0],154,69,"Abysmal Knight",2470,1,.@label$;
	monster 'map_name$[0],145,79,"Abysmal Knight",2470,1,.@label$;
	monster 'map_name$[0],154,79,"Abysmal Knight",2470,1,.@label$;
	monster 'map_name$[0],145,89,"Abysmal Knight",2470,1,.@label$;
	monster 'map_name$[0],154,89,"Abysmal Knight",2470,1,.@label$;
	monster 'map_name$[0],145,99,"Abysmal Knight",2470,1,.@label$;
	monster 'map_name$[0],154,99,"Abysmal Knight",2470,1,.@label$;

	monster 'map_name$[0],145,54,"Khalitzburg",2471,1,.@label$;// G_MG_KHALITZBURG
	unittalk $@mobid[0],"น้ำ ... มีคนให้น้ำ ...";
	monster 'map_name$[0],154,54,"Khalitzburg",2471,1,.@label$;
	unittalk $@mobid[0],"กร๊ากกกกกกกกก ...";
	monster 'map_name$[0],145,64,"Khalitzburg",2471,1,.@label$;
	unittalk $@mobid[0],"เซอร์ไฮน์ริช ช่วยท่านด้วย...";
	monster 'map_name$[0],154,64,"Khalitzburg",2471,1,.@label$;
	unittalk $@mobid[0],"คิดถึงพี่สาว ...";
	monster 'map_name$[0],145,74,"Khalitzburg",2471,1,.@label$;
	unittalk $@mobid[0],"อย่าทิ้งข้าไว้คนเดียว ช่วยข้าด้วย";
	monster 'map_name$[0],154,74,"Khalitzburg",2471,1,.@label$;
	unittalk $@mobid[0],"แอร๊ยรรรรรรรรรรร";
	monster 'map_name$[0],145,84,"Khalitzburg",2471,1,.@label$;
	unittalk $@mobid[0],"ฉันกระหายน้ำมาก";
	monster 'map_name$[0],154,84,"Khalitzburg",2471,1,.@label$;
	unittalk $@mobid[0],"โอ้ ... ไม่ ... ข้ายังตายไม่ได้ ...";
	monster 'map_name$[0],145,94,"Khalitzburg",2471,1,.@label$;
	unittalk $@mobid[0],"ข้ารู้สึกไม่สบายที่กระเพาะอาหารของข้า";
	monster 'map_name$[0],154,94,"Khalitzburg",2471,1,.@label$;
	unittalk $@mobid[0],"สักคนได้ไหม ...";
	monster 'map_name$[0],145,104,"Khalitzburg",2471,1,.@label$;
	unittalk $@mobid[0],"Arrgg ... ร่างกายของข้า";
	monster 'map_name$[0],154,104,"Khalitzburg",2471,1,.@label$;
	unittalk $@mobid[0],"แสบคอ!";
	end;
OnTimer94000:
	npctalk "Heinrich: ผู้ชายของข้า ... สิ่งนี้จะเกิดขึ้นไม่ได้!", 'npc_heinrich2$;
	end;
OnTimer96500:
	npctalk "Varmundt: เซอร์ไฮน์ริชพวกมันเป็นสัตว์ประหลาดทั้งหมด ท่านต้องให้พวกเขาพักผ่อน!", 'npc_varmundt1$;
	end;
OnTimer100000:
	npctalk "Heinrich: ข้าขอโทษ ....", 'npc_heinrich2$;
	end;
OnTimer103000:
	npctalk "Heinrich: ข้าขอโทษอัศวินของข้า! ยกโทษให้ข้าด้วย!", 'npc_heinrich2$;
	end;
OnTimer106000:
	mapannounce 'map_name$[0], "Sir Heinrich: ตายทุกคน!", bc_map,0xFFFF33,FW_NORMAL,15;
	end;
OnTimer109000:
	mapannounce 'map_name$[0], "Sir Heinrich: กลับสู่ความมืด!", bc_map,0xFFFF33,FW_NORMAL,15;
	end;
OnTimer109500:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#1");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#2");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#1");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#2");
	end;
OnTimer110000:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#3");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#4");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#5");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#6");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#3");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#4");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#5");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#6");
	end;
OnTimer110500:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#7");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#8");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#9");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#10");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#7");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#8");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#9");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#10");
	end;
OnTimer111000:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#11");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#12");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#13");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#14");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#11");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#12");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#13");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#14");
	end;
OnTimer111500:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#15");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#16");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#17");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#18");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#15");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#16");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#17");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#18");
	end;
OnTimer112000:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#19");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("White Knight#20");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#21");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Khalitzburg Knightage#22");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#19");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("White Knight#20");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#21");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Khalitzburg Knightage#22");
	disablenpc 'npc_heinrich2$;
	enablenpc 'npc_heinrich3$;
	killmonster 'map_name$[0], instance_npcname("#talkinstance1") + "::OnMyMobDead";
	end;
OnTimer112500:
	npctalk "Heinrich: ข้าฆ่าคนของตัวเอง ...", 'npc_heinrich3$;
	end;
OnTimer115500:
	npctalk "Varmundt: เซอร์ไฮน์ริช! เรามีเวลาไม่มาก!", 'npc_varmundt1$;
	end;
OnTimer118500:
	npctalk "Varmundt: ถ้าท่านรีบตอนนี้มีโอกาส!", 'npc_varmundt1$;
	end;
OnTimer121500:
	npctalk "Heinrich: Varmundt ถูกต้อง ตอนนี้ไม่ใช่เวลาไว้อาลัย", 'npc_heinrich3$;
	end;
OnTimer124500:
	npctalk "Heinrich: ข้ามีคำขอถึงท่านผู้ติดตาม", 'npc_heinrich3$;
	end;
OnTimer127500:
	npctalk "Heinrich: ฮิเมลเมซสามารถเปลี่ยนสิ่งมีชีวิตให้กลายเป็นสัตว์ประหลาดได้", 'npc_heinrich3$;
	end;
OnTimer130500:
	npctalk "Heinrich: แต่อาจยังมีผู้รอดชีวิตอยู่ที่นี่", 'npc_heinrich3$;
	end;
OnTimer134500:
	npctalk "Heinrich: ทำลายมอนสเตอร์และค้นหาผู้รอดชีวิต", 'npc_heinrich3$;
	end;
OnTimer138500:
	npctalk "Heinrich: Varmundt และข้าจะไล่ตาม Himelmez", 'npc_heinrich3$;
	end;
OnTimer143500:
	npctalk "Heinrich: ดีมาก Varmundt ไปหา Himelmez กันเถอะ", 'npc_heinrich3$;
	end;
OnTimer147500:
	disablenpc 'npc_heinrich3$;
	end;
OnTimer148500:
	stopnpctimer;
	donpcevent instance_npcname("#ghmemorialmob01") + "::OnStart";

	disablenpc 'npc_varmundt1$;
	disablenpc instance_npcname("#talkinstance1");
	'npc_himelmez1$ = 'npc_varmundt1$ = 'npc_heinrich2$ = 'npc_heinrich3$ = "";
	end;
OnMyMobDead:
	end;
}

// Sector 1 Mobs
//============================================================
1@gl_k,1,1,0	script	#ghmemorialmob01	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob01");
	enablenpc instance_npcname("#2Control");
	enablenpc instance_npcname("#2Control2");
	mapannounce 'map_name$[0], "พอร์ทัลเปิดไปทางทิศตะวันตก", bc_map,0xFFFF00;

	// note: x17 MG_GHOUL / x17 MG_ZOMBIE / x17 MG_WRAITH
	.@label$ = instance_npcname("#ghmemorialmob01") + "::OnMyMobDead";
	areamonster 'map_name$[0],76,99,87,10,"Grand Chamberlain in pain",2466,14,.@label$;
	areamonster 'map_name$[0],67,39,12, 6,"Corrupted Monk",2465,17,.@label$;
	areamonster 'map_name$[0],67,39,12, 6,"Grand Chamberlain in pain",2466,3,.@label$;
	areamonster 'map_name$[0],32,75,51,58,"Corrupted Steward",2464,5,.@label$;
	areamonster 'map_name$[0],45,84,6,137,"Corrupted Steward",2464,12,.@label$;
	end;
OnMyMobDead:
	.@label$ = instance_npcname("#ghmemorialmob01") + "::OnMyMobDead";
	.@mob_dead_num = 51 - mobcount('map_name$[0],.@label$);
	if (.@mob_dead_num > 35) {
		mapannounce 'map_name$[0], "คำสาปของฮิเมลเมซอ่อนแอลงเรื่อย ๆ ค้นหาผู้รอดชีวิต!", bc_map,0xFFFFFF;
		killmonster 'map_name$[0], .@label$;
		enablenpc instance_npcname("Altar boy Domun#1");
		disablenpc instance_npcname("#ghmemorialmob01");
	}
	end;
}

1@gl_k,17,51,3	script	Altar boy Domun#1	4_M_KID1,{
	if (is_party_leader() == false) {
		mes "[Altar boy Domun]";
		mes "ช่วยข้า ช่วยข้าด้วย ...";
		close;
	}
	.@account_id = getcharid(3);
	.@player_name$ = strcharinfo(0);
	mes "[Altar boy Domun]";
	mes "ช่วยข้า! ช่วยข้าด้วย!!!";
	npctalk "ช่วยข้า! ช่วยข้าด้วย!!!";
	next;
	select("เดี๋ยวก่อน! ท่านเป็นผู้รอดชีวิตคนเดียว?");
	mes "[" + .@player_name$ + "]";
	mes "เดี๋ยวก่อน! ท่านเป็นผู้รอดชีวิตคนเดียว?";
	unittalk .@account_id, .@player_name$ + " : เดี๋ยวก่อน! ท่านเป็นผู้รอดชีวิตคนเดียว?";
	next;
	mes "[Altar boy Domun]";
	mes "มหาดเล็ก ... พระ ... พวกเขาทั้งหมดกลายเป็นสัตว์ประหลาด ข้าทำอะไรไม่ได้เลย";
	npctalk "Altar boy Domun: มหาดเล็ก ... พระ ... พวกเขาทั้งหมดกลายเป็นสัตว์ประหลาด ข้าทำอะไรไม่ได้เลย";
	next;
	mes "[Altar boy Domun]";
	mes "ทั้งหมดที่ข้าทำได้คือ ... แค่ซ่อนตัวอยู่ที่นี่ ... ไม่มีอะไรฉันทำอะไรไม่ได้ .";
	npctalk "Altar boy Domun: ทั้งหมดที่ข้าทำได้คือ ... แค่ซ่อนตัวอยู่ที่นี่ ... ไม่มีอะไรฉันทำอะไรไม่ได้ .";
	next;
	select("ดึงมันเข้าด้วยกัน!");
	mes "[" + .@player_name$ + "]";
	mes "ไปทางตะวันออกแล้วหาทางเดินตรงกลางออกไปข้างนอก! ที่นั่นปลอดภัย!";
	unittalk .@account_id, .@player_name$ + " : ไปทางตะวันออกแล้วหาทางเดินตรงกลางออกไปข้างนอก! ที่นั่นปลอดภัย!";
	next;
	mes "[Altar boy Domun]";
	mes "ไปทางทิศตะวันออก? คนเดียว? อย่างไร?";
	npctalk "Altar boy Domun: ไปทางทิศตะวันออก? คนเดียว? อย่างไร?";
	next;
	select("ข้าจะให้อาวุธ");
	mes "[" + .@player_name$ + "]";
	mes "นี่คืออาวุธ เพียงแค่หลับตาและแกว่งไปหามอนสเตอร์เหล่านั้น";
	unittalk .@account_id, .@player_name$ + " : นี่คืออาวุธ เพียงแค่หลับตาและแกว่งไปหามอนสเตอร์เหล่านั้น";
	next;
	mes "[Altar boy Domun]";
	mes "ตกลงข้า ... ข้าจะพยายาม";
	npctalk "Altar boy Domun: ตกลงข้า ... ข้าจะพยายาม";
	disablenpc instance_npcname("Altar boy Domun#1");
	donpcevent instance_npcname("#ghmemorialmob02") + "::OnStart";
	close;
}

// Sector 2 Mobs
//============================================================
1@gl_k,1,1,0	script	#ghmemorialmob02	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob02");
	enablenpc instance_npcname("#3Control");
	enablenpc instance_npcname("#3Control2");
	for ( .@i = 1; .@i <= 26; .@i++ )
		enablenpc instance_npcname("A dead man#" + .@i);
	mapannounce 'map_name$[0], "พอร์ทัลเปิดไปทางทิศตะวันออก", bc_map,0xFFFF00;

	// note: x34 MG_ZOMBIE / x35 MG_GHOUL
	.@label$ = instance_npcname("#ghmemorialmob02") + "::OnMyMobDead";
	areamonster 'map_name$[0],236,12,288,52,"Outraged Refiner",2466,18,.@label$;// MG_GHOUL
	areamonster 'map_name$[0],236,12,288,52,"Decomposed Blacksmith",2464,17,.@label$;// MG_ZOMBIE
	areamonster 'map_name$[0],242,71,286,145,"Outraged Refiner",2466,17,.@label$;
	areamonster 'map_name$[0],242,71,286,145,"Decomposed Blacksmith",2464,17,.@label$;
	end;
OnMyMobDead:
	.@label$ = instance_npcname("#ghmemorialmob02") + "::OnMyMobDead";
	.@mob_dead_num = 69 - mobcount('map_name$[0],.@label$);
	if (.@mob_dead_num > 56) {
		mapannounce 'map_name$[0], "คำสาปของฮิเมลเมซอ่อนแอลงเรื่อย ๆ ค้นหาผู้รอดชีวิต!", bc_map,0xFFFFFF;
		killmonster 'map_name$[0],.@label$;
		enablenpc instance_npcname("Holgren the Destroyer#1");
		disablenpc instance_npcname("#ghmemorialmob02");
	}
	end;
}

1@gl_k,291,145,3	script	Holgren the Destroyer#1	4_F_JOB_BLACKSMITH,{
	if (is_party_leader() == false) {
		mes "[Holgren the Destroyer]";
		mes "เจ้าเป็นมนุษย์?";
		close;
	}
	.@account_id = getcharid(3);
	.@player_name$ = strcharinfo(0);
	mes "[Holgren the Destroyer]";
	mes "ตาย!! ตาย !!!";
	npctalk "Holgren the Destroyer: ตาย!! ตาย !!!";
	specialeffect EF_CRASHEARTH;
	next;
	select("ใจเย็นก่อน ข้าไม่ใช่สัตว์ประหลาด!");
	mes "[" + .@player_name$ + "]";
	mes "ใจเย็นก่อน ข้าเป็นมนุษย์ เจ้าเหรออยู่คนเดียว? มีใครอยู่ที่นี่อีกไหม?";
	unittalk .@account_id, .@player_name$ + " : ใจเย็นก่อน ข้าเป็นมนุษย์ เจ้าเหรออยู่คนเดียว? มีใครอยู่ที่นี่อีกไหม?";
	next;
	mes "[Holgren the Destroyer]";
	mes "ข้าคนเดียว";
	npctalk "Holgren the Destroyer: ข้าคนเดียว";
	next;
	select("It's dangerous here...");
	mes "[" + .@player_name$ + "]";
	mes "มันอันตรายที่นี่ ท่านรู้ทางไปสู่ทางสายกลางหรือไม่? ย้ายได้มั้ย?";
	unittalk .@account_id, .@player_name$ + " : มันอันตรายที่นี่ ท่านรู้ทางไปสู่ทางสายกลางหรือไม่? ย้ายได้มั้ย?";
	next;
	mes "[Holgren the Destroyer]";
	mes "ใช่ ข้าสามารถย้ายได้ ข้าต้องออกไปจากที่นี่";
	npctalk "Holgren the Destroyer: ใช่ ข้าสามารถย้ายได้ ข้าต้องออกไปจากที่นี่";
	next;
	select("Be safe...");
	mes "[" + .@player_name$ + "]";
	mes "ขอโทษ ข้าไปกับเจ้าไม่ได้";
	unittalk .@account_id, .@player_name$ + " : ขอโทษ ข้าไปกับเจ้าไม่ได้";
	next;
	mes "[Holgren the Destroyer]";
	mes "ไม่เป็นไร. ท่านได้ทำเพียงพอแล้ว ข้าสามารถช่วยตัวเองได้ ขอให้โชคดีเช่นกัน";
	npctalk "Holgren the Destroyer: ไม่เป็นไร. ท่านได้ทำเพียงพอแล้ว ข้าสามารถช่วยตัวเองได้ ขอให้โชคดีเช่นกัน";
	disablenpc instance_npcname("Holgren the Destroyer#1");
	donpcevent instance_npcname("#ghmemorialmob03") + "::OnEnable";
	close;
}

// Tramp Mobs
//============================================================
1@gl_k,221,82,3	script	A dead man#1	4_M_DIEMAN,5,5,{
	end;
OnTouch_:
	.@i = rand(1,10);
	if (.@i == 1) .@mobs = 3;
	else if (.@i == 2) .@mobs = 4;
	else if (.@i == 3) .@mobs = 5;
	else if (.@i < 7) .@mobs = 6;
	else .@mobs = 7;
	getmapxy .@map$,.@x,.@y, BL_NPC;
	specialeffect EF_VENOMDUST;
	monster .@map$,.@x,.@y,"Maggot",2467,.@mobs;
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}
1@gl_k,233,123,4	duplicate(A dead man#1)	A dead man#2	4_M_DIEMAN,5,5
1@gl_k,258,150,2	duplicate(A dead man#1)	A dead man#3	4_M_DIEMAN,5,5
1@gl_k,255,157,5	duplicate(A dead man#1)	A dead man#4	4_M_DIEMAN,5,5
1@gl_k,280,167,4	duplicate(A dead man#1)	A dead man#5	4_M_DIEMAN,5,5
1@gl_k,293,161,2	duplicate(A dead man#1)	A dead man#6	4_M_DIEMAN,5,5
1@gl_k,249,101,3	duplicate(A dead man#1)	A dead man#7	4_M_DIEMAN,5,5
1@gl_k,241,86,6	duplicate(A dead man#1)	A dead man#8	4_M_DIEMAN,5,5
1@gl_k,246,62,0	duplicate(A dead man#1)	A dead man#9	4_M_DIEMAN,5,5
1@gl_k,240,43,7	duplicate(A dead man#1)	A dead man#10	4_M_DIEMAN,5,5
1@gl_k,271,19,1	duplicate(A dead man#1)	A dead man#11	4_M_DIEMAN,5,5
1@gl_k,282,48,5	duplicate(A dead man#1)	A dead man#12	4_M_DIEMAN,5,5
1@gl_k,285,81,7	duplicate(A dead man#1)	A dead man#13	4_M_DIEMAN,5,5
1@gl_k,276,106,0	duplicate(A dead man#1)	A dead man#14	4_M_DIEMAN,5,5
1@gl_k,261,164,0	duplicate(A dead man#1)	A dead man#15	4_M_DIEMAN,5,5
1@gl_k,269,173,0	duplicate(A dead man#1)	A dead man#16	4_M_DIEMAN,5,5
1@gl_k,252,120,0	duplicate(A dead man#1)	A dead man#17	4_M_DIEMAN,5,5
1@gl_k,213,63,0	duplicate(A dead man#1)	A dead man#18	4_M_DIEMAN,5,5
1@gl_k,222,39,0	duplicate(A dead man#1)	A dead man#19	4_M_DIEMAN,5,5
1@gl_k,214,27,0	duplicate(A dead man#1)	A dead man#20	4_M_DIEMAN,5,5
1@gl_k,223,17,1	duplicate(A dead man#1)	A dead man#21	4_M_DIEMAN,5,5
1@gl_k,230,50,2	duplicate(A dead man#1)	A dead man#22	4_M_DIEMAN,5,5
1@gl_k,235,16,3	duplicate(A dead man#1)	A dead man#23	4_M_DIEMAN,5,5
1@gl_k,226,96,4	duplicate(A dead man#1)	A dead man#24	4_M_DIEMAN,5,5
1@gl_k,222,119,5	duplicate(A dead man#1)	A dead man#25	4_M_DIEMAN,5,5
1@gl_k,251,20,6	duplicate(A dead man#1)	A dead man#26	4_M_DIEMAN,5,5

// Sector 3 Mobs
//============================================================
1@gl_k,1,1,0	script	#ghmemorialmob03	HIDDEN_WARP_NPC,{
	end;
OnEnable:
	enablenpc instance_npcname("#ghmemorialmob03");
	enablenpc instance_npcname("#4Control");
	enablenpc instance_npcname("#4Control2");
	mapannounce 'map_name$[0], "พอร์ทัลปรากฏขึ้นทางทิศตะวันตกเฉียงเหนือ", bc_map,0xFFFF00;

	// note: x50 MG_RAYDRIC / x50 MG_RAYDRIC_ARCHER
	.@label$ = instance_npcname("#ghmemorialmob03") + "::OnMyMobDead";
	areamonster 'map_name$[0],16,186,43,280,"Outraged Archer",2469,25,.@label$;
	areamonster 'map_name$[0],44,191,89,225,"Hungry Palace Guard",2468,25,.@label$;
	areamonster 'map_name$[0],115,215,188,273,"Corrupted Palace Guard",2468,20,.@label$;
	areamonster 'map_name$[0],108,232,193,281,"Corrupted Palace Guard",2468,5,.@label$;
	areamonster 'map_name$[0],158,236,230,250,"Wandering Archer",2469,12,.@label$;
	areamonster 'map_name$[0],65,231,155,256,"Wandering Archer",2469,13,.@label$;
	end;
OnMyMobDead:
	.@label$ = instance_npcname("#ghmemorialmob03") + "::OnMyMobDead";
	.@mob_dead_num = 100 - mobcount('map_name$[0],.@label$);
	if (.@mob_dead_num > 85) {
		killmonster 'map_name$[0],.@label$;
		enablenpc instance_npcname("#Mimelon");
		enablenpc instance_npcname("Himelmez#2");
		enablenpc instance_npcname("Varmundt#2");
		enablenpc instance_npcname("Heinrich#4");
		disablenpc instance_npcname("#ghmemorialmob03");
		mapannounce 'map_name$[0], "Himelmez: ไม่เลว คิดว่าตอนนี้เจ้าจะตายเพราะสิ่งมีชีวิตของข้า ~", bc_map,0xFFFFFF;
		initnpctimer;
	}
	end;
OnTimer5000:
	mapannounce 'map_name$[0], "Himelmez: ข้าจะรอเจ้าที่ทางเหนือของปราสาท ข้าอยากเห็นว่าเจ้าโชคดีแค่ไหน", bc_map,0xFFFFFF;
	stopnpctimer;
	end;
}

// 1st MVP
//============================================================
1@gl_k,144,258,6	script	Heinrich#4	4_M_HEINRICH,{
	mes "[Heinrich]";
	mes "ฮิเมลเมซ ... ข้าจะไม่มีวันลืมสิ่งที่เจ้าทำกับผู้ชายของข้า";
	cutin "gl_heinrich1",2;
	close3;
}

// note: never hidden
1@gl_k,156,258,3	script	Varmundt#2	4_M_BARMUND,{
	cutin "gl_barmund1",2;
	mes "[Varmundt]";
	if (checkquest(12318,HUNTING) != 2) {
		mes "ข้าเดาว่านี่คือมัน เป็นไปไม่ได้หรือที่จะหยุดความตั้งใจของนักท่องเวลา!";
		mes "ดูเหมือนว่าพอร์ทัลจะปรากฏขึ้นทางทิศเหนือ";
		close3;
	}
	erasequest 12318;
	if (isbegin_quest(12319) == 0)
		setquest 12319;// Amdarais Hunt
	if (isbegin_quest(12320) == 0) {
		setquest 12320;// Time Traveler
		completequest 12320;
		getexp 250000,250000;
	}
	mes "ไอเท็มนี้มีออร่าที่ไม่ธรรมดาด้วย จะมีประโยชน์มากสำหรับคนพิเศษ";
	getitem 6607,1;// Temporal_Crystal
	getitem 6608,1;// Coagulated_Spell
	close3;
}

1@gl_k,150,257,3	script	Himelmez#2	4_F_HIMEL,{
	cutin "gl_himel2",2;
	mes "[Himelmez]";
	mes "อย่ามองข้าประหม่าแบบนั้น มันจะจบลงในไม่ช้า ...";
	close3;
}

1@gl_k,150,257,0	script	#Mimelon	HIDDEN_WARP_NPC,7,7,{
	end;
OnTouch_:
	disablenpc instance_npcname("#Mimelon");
	initnpctimer;
	'npc_himelmez2$ = instance_npcname("Himelmez#2");
	'npc_heinrich4$ = instance_npcname("Heinrich#4");
	end;
OnTimer3000:
	npctalk "Himelmez: เดาว่าเจ้าโชคดีหรือมีพลังที่ทำให้มันมาไกลได้", 'npc_himelmez2$;
	end;
OnTimer6000:
	npctalk "Himelmez: แต่มันไม่สำคัญ", 'npc_himelmez2$;
	end;
OnTimer9000:
	npctalk "Himelmez: พวกเจ้าทั้งหมดจะต้องตายที่นี่", 'npc_himelmez2$;
	end;
OnTimer12000:
	npctalk "Heinrich: ฮิเมลเมซ! เราจะไม่ปล่อยให้เจ้าห่างจากเราไป!", 'npc_heinrich4$;
	end;
OnTimer15000:
	npctalk "Himelmez: ฮ่า ๆ ๆ ห่วงตัวเองก่อนเถอะ เจ้าคิดว่าข้าอยู่คนเดียวที่นี่เหรอ?", 'npc_himelmez2$;
	end;
OnTimer18000:
	npctalk "Varmundt: เซอร์ไฮน์ริช! ข้ารู้สึกได้ถึงบางอย่างที่แปลกประหลาดและมันกำลังมา!", instance_npcname("Varmundt#2");
	end;
OnTimer21000:
	npctalk "Heinrich: อะไรกัน ... พวกนี้!", 'npc_heinrich4$;
	end;
OnTimer24000:
	npctalk "Himelmez: ข้าจะไปในขณะที่ของเล่นใหม่ของข้าให้ความบันเทิงกับเจ้า", 'npc_himelmez2$;
	end;
OnTimer27000:
	npctalk "Himelmez: เดินทางให้ดีนะ ~ หวังว่าจะได้พบเจ้าอีกครั้งไฮน์ริช", 'npc_himelmez2$;
	end;
OnTimer28000:
	disablenpc 'npc_himelmez2$;
	end;
OnTimer31000:
	npctalk "Heinrich: Sir Varmundt! ช่วยเหลือเหล่านักผจญภัย ฉันจะไปตามฮิเมลเมซ!", 'npc_heinrich4$;
	end;
OnTimer32000:
	disablenpc 'npc_heinrich4$;
	end;
OnTimer35000:
	mapannounce 'map_name$[0], "เสียงสะท้อนดังมาจากด้านในของศาลเจ้าอัศวิน", bc_map,0xFFFFFF;
	end;
OnTimer38000:
	donpcevent instance_npcname("#ghmemorialmob04") + "::OnStart";
	stopnpctimer;
	'npc_himelmez2$ = 'npc_heinrich4$ = "";
	end;
}

1@gl_k,1,1,0	script	#ghmemorialmob04	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob04");
	monster 'map_name$[0],150,259,"Corrupted Soul",2475,1, instance_npcname("#ghmemorialmob04") + "::OnMyMobDead";// MG_CORRUPTION_ROOT
	unittalk $@mobid[0],"Grrrrrrhh~~~";
	end;
OnMyMobDead:
	if (mobcount('map_name$[0], instance_npcname("#ghmemorialmob04") + "::OnMyMobDead") < 1) {
		mapannounce 'map_name$[0], "เปิดทางเข้าชั้น 2 ไปทาง 12 นาฬิกา", bc_map,0xFFFF00;
		npctalk "Varmundt: ข้าเก็บของบางอย่างที่หล่นจากตัวของมัน ท่านสามารถเอามันจากข้าได้", instance_npcname("Varmundt#2");

		enablenpc instance_npcname("#2F Entrance");
		enablenpc instance_npcname("#1 Control");
		enablenpc instance_npcname("#22 Control");
		enablenpc instance_npcname("#22 Control2");
		enablenpc instance_npcname("#Servanton");
		enablenpc instance_npcname("Heinrich#21");
		enablenpc instance_npcname("Varmundt#21");
		donpcevent instance_npcname("#ghmemorialmob05") + "::OnStart";
		disablenpc instance_npcname("#ghmemorialmob04");
	}
	end;
}

// Floor 2
//============================================================
2@gl_k,148,67,1	script	Heinrich#21	4_M_HEINRICH,{
	mes "[Heinrich]";
	mes "พื้นที่ปิดของ Himelmez ครอบคลุมทุกที่ ...";
	cutin "gl_heinrich1",2;
	close3;
}

2@gl_k,151,71,7	script	Varmundt#21	4_M_BARMUND,{
	mes "[Varmundt]";
	mes "ข้าทำงานนี้มากี่ครั้งแล้ว? ในความฝันข้าทำมันซ้ำแล้วซ้ำเล่า ...";
	cutin "gl_barmund2",2;
	close3;
}

2@gl_k,150,66,0	script	#Servanton_effect	HIDDEN_WARP_NPC,{
	end;
OnEffect:
	specialeffect EF_LORD;
	end;
}

// note: this part can be skipped
2@gl_k,150,66,0	script	#Servanton	HIDDEN_WARP_NPC,7,7,{
	end;
OnTouch_:
	disablenpc instance_npcname("#Servanton");
	initnpctimer;
	'npc_heinrich$ = instance_npcname("Heinrich#21");
	'npc_varmundt$ = instance_npcname("Varmundt#21");
	end;
OnTimer3000:
	npctalk "Heinrich: โครงสร้างนี้ไม่เคยอยู่ในปราสาทมาก่อน!", 'npc_heinrich$;
	end;
OnTimer6000:
	npctalk "Varmundt: นี่คือพื้นที่ปิดของ Himelmez เราปิดกั้นแต่ละพื้นที่และทางเดิน", 'npc_varmundt$;
	end;
OnTimer9000:
	npctalk "Heinrich: เราพยายามทำลายมันลง แต่มันแข็งแกร่งจริงๆ", 'npc_heinrich$;
	end;
OnTimer12000:
	npctalk "Varmundt: ถอยหลัง. ข้าพยายามที่จะปัดเป่าเวทมนตร์", 'npc_varmundt$;
	end;
OnTimer15000:
	specialeffect EF_LORD,AREA, instance_npcname("#Servanton_effect");
	disablenpc instance_npcname("#Servanton_effect");
	end;
OnTimer18000:
	npctalk "Varmundt: ข้าคิดว่าพื้นที่ปิดส่วนนั้นถูกทำลายลง", 'npc_varmundt$;
	end;
OnTimer21000:
	npctalk "Varmundt: ข้าไม่เคยเห็นพื้นที่ปิดนี้", 'npc_varmundt$;
	end;
OnTimer24000:
	npctalk "Varmundt: ฮิเมลเมซซ่อนโซ่หินไว้ในร่างของคนที่ยังไม่ตาย", 'npc_varmundt$;
	end;
OnTimer27000:
	npctalk "Varmundt: บางทีพื้นที่ปิดนี้ได้รับการบำรุงรักษาด้วยโซ่หินเหล่านี้", 'npc_varmundt$;
	end;
OnTimer30000:
	npctalk "Varmundt: หากท่านต้องการทำลายมัน ท่านต้องฆ่าหนึ่งในผู้ที่มีโซ่หิน", 'npc_varmundt$;
	end;
OnTimer33000:
	npctalk "Varmundt: แต่เราไม่สามารถรับรู้ได้ว่าใครมีโซ่หินดังนั้นเราจึงต้องชำระล้างทุกอย่าง", 'npc_varmundt$;
	end;
OnTimer36000:
	npctalk "Heinrich: เป็นพฤติกรรมที่หลีกเลี่ยงไม่ได้", 'npc_heinrich$;
	end;
OnTimer39000:
	npctalk "Heinrich: ข้ากำจัดทหารทั้งหมดของข้าไปแล้วและอาจจะมากกว่านั้น ...", 'npc_heinrich$;
	end;
OnTimer42000:
	npctalk "Varmundt: ผู้บัญชาการ ...", 'npc_varmundt$;
	end;
OnTimer45000:
	npctalk "Varmundt: ผู้บัญชาการ! ท่านต้องเข้มแข็ง", 'npc_varmundt$;
	end;
OnTimer48000:
	npctalk "Varmundt: ศัตรูของเราไม่ใช่มนุษย์", 'npc_varmundt$;
	end;
OnTimer51000:
	npctalk "Varmundt: แม้ว่าท่านจะปฏิเสธ แต่พวกเขาก็ไม่มีวันกลับไปมาเป็นมนุษย์", 'npc_varmundt$;
	end;
OnTimer54000:
	npctalk "Heinrich: ...", 'npc_heinrich$;
	end;
OnTimer57000:
	npctalk "Varmundt: ไปกันเลยดีกว่า", 'npc_varmundt$;
	end;
OnTimer60000:
	npctalk "Varmundt: และพวกท่านเพียงแค่หยุดพักและติดตามเรา", 'npc_varmundt$;
	end;
OnTimer63000:
	npctalk "Varmundt: อาจเป็นการต่อสู้ที่ยากลำบากดังนั้นจงเข้มแข็งไว้", 'npc_varmundt$;
	end;
OnTimer66000:
	npctalk "Varmundt: ได้เวลาเริ่มไฮน์ริชแล้ว", 'npc_varmundt$;
	end;
OnTimer69000:
	mapannounce 'map_name$[1], "ประตูทางทิศตะวันตกเปิดขึ้นที่โถงทางเดินกลาง", bc_map,0xFFFF00;
	disablenpc 'npc_varmundt$;
	disablenpc 'npc_heinrich$;
	stopnpctimer;
	'npc_varmundt$ = 'npc_heinrich$ = "";
	end;
}

// Spots
// Note: timer is the only condition for them to respawn
2@gl_k,118,141,0	script	#ogh_2-1	HIDDEN_WARP_NPC,20,20,{
	end;
OnTouch_:
	disablenpc instance_npcname( strnpcinfo(0) );
	.@hidden_name$ = strnpcinfo(2);
	.@event_type = atoi( charat(.@hidden_name$,4) );
	if (.@event_type == 2)
		.@label$ = instance_npcname("#ghmemorialmob05") + "::OnMyMobDead";
	else
		.@label$ = instance_npcname("#ghmemorialmob06") + "::OnMyMobDead";
	getmapxy .@map$,.@x,.@y, BL_NPC;
	monster .@map$,.@x,.@y, "Corrupted Palace Guard", 2468,1, .@label$;
	monster .@map$,.@x,.@y, "Archer of Death", 2469,1, .@label$;
	monster .@map$,.@x,.@y, "Corrupted Abysmal Knight", 2470,1, .@label$;
	monster .@map$,.@x,.@y, "Suffered Khalitzburg", 2471,1, .@label$;
	monster .@map$,.@x,.@y, "Bloody Knight", 2472,1, .@label$;
	initnpctimer;
	end;
OnTimer30000:
	enablenpc instance_npcname( strnpcinfo(0) );
	stopnpctimer;
	end;
OnStop:
	disablenpc instance_npcname( strnpcinfo(0) );
	stopnpctimer;
	end;
}
2@gl_k,128,81,0	duplicate(#ogh_2-1)	#ogh_2-2	HIDDEN_WARP_NPC,20,20
2@gl_k,131,54,0	duplicate(#ogh_2-1)	#ogh_2-3	HIDDEN_WARP_NPC,20,20
2@gl_k,89,48,0	duplicate(#ogh_2-1)	#ogh_2-4	HIDDEN_WARP_NPC,20,20
2@gl_k,64,117,0	duplicate(#ogh_2-1)	#ogh_2-5	HIDDEN_WARP_NPC,20,20
2@gl_k,62,82,0	duplicate(#ogh_2-1)	#ogh_2-6	HIDDEN_WARP_NPC,20,20
2@gl_k,38,138,0	duplicate(#ogh_2-1)	#ogh_2-7	HIDDEN_WARP_NPC,20,20

2@gl_k,171,120,0	duplicate(#ogh_2-1)	#ogh_3-1	HIDDEN_WARP_NPC,20,20
2@gl_k,232,133,0	duplicate(#ogh_2-1)	#ogh_3-2	HIDDEN_WARP_NPC,20,20
2@gl_k,256,149,0	duplicate(#ogh_2-1)	#ogh_3-3	HIDDEN_WARP_NPC,20,20
2@gl_k,212,106,0	duplicate(#ogh_2-1)	#ogh_3-4	HIDDEN_WARP_NPC,20,20
2@gl_k,243,73,0	duplicate(#ogh_2-1)	#ogh_3-5	HIDDEN_WARP_NPC,20,20
2@gl_k,229,26,0	duplicate(#ogh_2-1)	#ogh_3-6	HIDDEN_WARP_NPC,20,20
2@gl_k,181,34,0	duplicate(#ogh_2-1)	#ogh_3-7	HIDDEN_WARP_NPC,20,20

// Commander 1
2@gl_k,1,1,0	script	#ghmemorialmob05	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob05");
	for ( .@i = 1; .@i <= 7; .@i++ )
		enablenpc instance_npcname("#ogh_2-" + .@i);
	end;

OnMyMobDead:
	if (rand(20) == 0) {// can re-spawn
		mapannounce 'map_name$[1], "กองกำลังชั่วร้ายกำลังปรากฏตัวในพื้นที่นี้", bc_map,0xFFFF44,FW_NORMAL,15;
		killmonster 'map_name$[1], instance_npcname("#ghmemorialmob05") + "::OnBossDead";

		.@r = rand(4) * 2;
		setarray .@coord[0], 41,146, 58,44, 122,148, 131,64;
		monster 'map_name$[1],.@coord[.@r],.@coord[.@r+1], "1st Commander of Destruction",2473,1, instance_npcname("#ghmemorialmob05") + "::OnBossDead";
	}
	end;

OnBossDead:
	mapannounce 'map_name$[1], "พอร์ทัลเปิดไปทางทิศตะวันออกในโถงทางเดินกลาง", bc_map,0xFFFF00;
	enablenpc instance_npcname("#23 Control");
	enablenpc instance_npcname("#23 Control2");
	donpcevent instance_npcname("#ghmemorialmob06") + "::OnStart";

	disablenpc instance_npcname("#ghmemorialmob05");
	for ( .@i = 1; .@i <= 7; .@i++ )
		donpcevent instance_npcname("#ogh_2-" + .@i) + "::OnStop";
	end;
}

// Commander 2
2@gl_k,1,1,0	script	#ghmemorialmob06	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob06");
	for ( .@i = 1; .@i <= 7; .@i++ )
		enablenpc instance_npcname("#ogh_3-" + .@i);

	// Fix spawn
	.@label$ = instance_npcname("#ghmemorialmob06") + "::OnMyMobDead";
	monster 'map_name$[1],252, 75, "Corrupted Palace Guard",2468,1, .@label$;
	monster 'map_name$[1],253, 76, "Corrupted Palace Guard",2468,1, .@label$;
	monster 'map_name$[1],247, 77, "Corrupted Palace Guard",2468,1, .@label$;
	monster 'map_name$[1],248, 80, "Corrupted Palace Guard",2468,1, .@label$;
	monster 'map_name$[1],236,100, "Corrupted Palace Guard",2468,1, .@label$;
	monster 'map_name$[1],240,100, "Corrupted Palace Guard",2468,1, .@label$;
	monster 'map_name$[1],242,100, "Corrupted Palace Guard",2468,1, .@label$;
	monster 'map_name$[1],244,100, "Corrupted Palace Guard",2468,1, .@label$;
	monster 'map_name$[1],199,111, "Corrupted Palace Guard",2468,1, .@label$;
	monster 'map_name$[1],181,107, "Wandering Archer",2469,1, .@label$;
	monster 'map_name$[1],177,110, "Wandering Archer",2469,1, .@label$;
	monster 'map_name$[1],236, 27, "Wandering Archer",2469,1, .@label$;
	monster 'map_name$[1],242, 27, "Wandering Archer",2469,1, .@label$;
	monster 'map_name$[1],252, 26, "Wandering Archer",2469,1, .@label$;
	monster 'map_name$[1],172,130, "Wandering Archer",2469,1, .@label$;
	monster 'map_name$[1],171,127, "Wandering Archer",2469,1, .@label$;
	monster 'map_name$[1],173,127, "Wandering Archer",2469,1, .@label$;
	monster 'map_name$[1],177, 69, "Corrupted Abysmal Knight",2470,1, .@label$;
	monster 'map_name$[1],186, 66, "Corrupted Abysmal Knight",2470,1, .@label$;
	monster 'map_name$[1],189, 67, "Corrupted Abysmal Knight",2470,1, .@label$;
	monster 'map_name$[1],190, 68, "Corrupted Abysmal Knight",2470,1, .@label$;
	monster 'map_name$[1],257,157, "Corrupted Abysmal Knight",2470,1, .@label$;
	monster 'map_name$[1],246,159, "Corrupted Abysmal Knight",2470,1, .@label$;
	monster 'map_name$[1],237,158, "Corrupted Abysmal Knight",2470,1, .@label$;
	monster 'map_name$[1],206,159, "Corrupted Abysmal Knight",2470,1, .@label$;
	monster 'map_name$[1],172,120, "Corrupted Abysmal Knight",2470,1, .@label$;
	monster 'map_name$[1],226, 30, "Suffered Khalitzburg",2471,1, .@label$;
	monster 'map_name$[1],226, 38, "Suffered Khalitzburg",2471,1, .@label$;
	monster 'map_name$[1],228, 29, "Suffered Khalitzburg",2471,1, .@label$;
	monster 'map_name$[1],226, 63, "Suffered Khalitzburg",2471,1, .@label$;
	monster 'map_name$[1],268,137, "Suffered Khalitzburg",2471,1, .@label$;
	monster 'map_name$[1],263,138, "Suffered Khalitzburg",2471,1, .@label$;
	monster 'map_name$[1],259,138, "Suffered Khalitzburg",2471,1, .@label$;
	monster 'map_name$[1],259,138, "Suffered Khalitzburg",2471,1, .@label$;
	monster 'map_name$[1],187,121, "Suffered Khalitzburg",2471,1, .@label$;
	monster 'map_name$[1],265, 52, "Bloody Knight",2472,1, .@label$;
	monster 'map_name$[1],265, 55, "Bloody Knight",2472,1, .@label$; 
	monster 'map_name$[1],264, 79, "Bloody Knight",2472,1, .@label$; 
	monster 'map_name$[1],225, 92, "Bloody Knight",2472,1, .@label$; 
	monster 'map_name$[1],237, 92, "Bloody Knight",2472,1, .@label$; 
	monster 'map_name$[1],249, 92, "Bloody Knight",2472,1, .@label$; 
	monster 'map_name$[1],256, 93, "Bloody Knight",2472,1, .@label$; 
	monster 'map_name$[1],264,100, "Bloody Knight",2472,1, .@label$;
	monster 'map_name$[1],181,120, "Bloody Knight",2472,1, .@label$;
	end;

OnMyMobDead:
	if (rand(50) == 0) {// can re-spawn
		mapannounce 'map_name$[1], "การปรากฏตัวที่ชั่วร้ายได้เคลื่อนย้ายเข้ามาในพื้นที่นี้", bc_map,0xFFFF44,FW_NORMAL,15;
		killmonster 'map_name$[1], instance_npcname("#ghmemorialmob06") + "::OnBossDead";

		.@r = rand(4) * 2;
		setarray .@coord[0], 166,119, 211,45, 227,139, 245,74;
		monster 'map_name$[1],.@coord[.@r],.@coord[.@r+1], "2nd Commander of Destruction",2474,1, instance_npcname("#ghmemorialmob06") + "::OnBossDead";
	}
	end;

OnBossDead:
	mapannounce 'map_name$[1], "พอร์ทัลใหม่ปรากฏขึ้นที่ส่วนท้ายของทางเดินกลาง", bc_map,0xFFFF00;
	enablenpc instance_npcname("#24 Control");
	enablenpc instance_npcname("#24 Control2");
	enablenpc instance_npcname("#Geron");
	for ( .@i = 1; .@i <= 35; .@i++ )
		enablenpc instance_npcname("Decomposed body#" + .@i);

	disablenpc instance_npcname("#ghmemorialmob06");
	for ( .@i = 1; .@i <= 7; .@i++ )
		donpcevent instance_npcname("#ogh_3-" + .@i) + "::OnStop";

	// Hidden mobs
	setarray .@coord[0],
		 37,265, 57,265, 77,265, 97,265, 117,265, 188,264, 208,264,
		 50,172, 70,172, 90,172, 110,172, 170,172, 210,172, 230,172,
		 88,214, 108,214, 128,214, 180,219, 200,219, 220,219, 240,219;
	.@size = getarraysize(.@coord);
	for ( .@i = 0; .@i < .@size; .@i += 2 )
		monster 'map_name$[1], .@coord[.@i], .@coord[.@i+1], "Flame of destruction",2337,1;// HIDDEN_MOB
	end;
}

// Amdarais Room Entrance
2@gl_k,150,180,0	script	#Geron	HIDDEN_WARP_NPC,7,7,{
	end;
OnTouch_:
	disablenpc instance_npcname("#Geron");
	mapannounce 'map_name$[1], "???: อย่ามาที่นี่! มันคือกับดัก ... กร๊ากกก !!!", bc_map,0xFF7777,FW_NORMAL,15;
	enablenpc instance_npcname("Himelmez#22");
	enablenpc instance_npcname("Varmundt#23");
	enablenpc instance_npcname("Heinrich#23");
	enablenpc instance_npcname("Gerhalt#23");
	end;
}

2@gl_k,143,260,4	script	Decomposed body#1	4_M_DIEMAN,5,5,{
	end;
OnTouch_:
	disablenpc instance_npcname( strnpcinfo(0) );
	.@i = rand(1,10);
	if (.@i == 1) .@mobs = 3;
	else if (.@i == 2) .@mobs = 4;
	else if (.@i == 3) .@mobs = 5;
	else if (.@i < 7) .@mobs = 6;
	else .@mobs = 7;
	getmapxy .@map$,.@x,.@y, BL_NPC;
	specialeffect EF_VENOMDUST;
	monster .@map$,.@x,.@y,"Maggot",2467,.@mobs;
	initnpctimer;
	end;
OnTimer45000:
	enablenpc instance_npcname( strnpcinfo(0) );
	stopnpctimer;
	end;
}
2@gl_k,145,236,4	duplicate(Decomposed body#1)	Decomposed body#2	4_M_DIEMAN,5,5
2@gl_k,141,222,2	duplicate(Decomposed body#1)	Decomposed body#3	4_M_DIEMAN,5,5
2@gl_k,147,203,5	duplicate(Decomposed body#1)	Decomposed body#4	4_M_DIEMAN,5,5
2@gl_k,167,225,4	duplicate(Decomposed body#1)	Decomposed body#5	4_M_DIEMAN,5,5
2@gl_k,172,233,2	duplicate(Decomposed body#1)	Decomposed body#6	4_M_DIEMAN,5,5
2@gl_k,176,244,3	duplicate(Decomposed body#1)	Decomposed body#7	4_M_DIEMAN,5,5
2@gl_k,184,248,6	duplicate(Decomposed body#1)	Decomposed body#8	4_M_DIEMAN,5,5
2@gl_k,193,228,0	duplicate(Decomposed body#1)	Decomposed body#9	4_M_DIEMAN,5,5
2@gl_k,206,250,7	duplicate(Decomposed body#1)	Decomposed body#10	4_M_DIEMAN,5,5
2@gl_k,130,249,1	duplicate(Decomposed body#1)	Decomposed body#11	4_M_DIEMAN,5,5
2@gl_k,122,236,5	duplicate(Decomposed body#1)	Decomposed body#12	4_M_DIEMAN,5,5
2@gl_k,130,228,7	duplicate(Decomposed body#1)	Decomposed body#13	4_M_DIEMAN,5,5
2@gl_k,106,226,0	duplicate(Decomposed body#1)	Decomposed body#14	4_M_DIEMAN,5,5
2@gl_k,104,245,0	duplicate(Decomposed body#1)	Decomposed body#15	4_M_DIEMAN,5,5
2@gl_k,131,187,0	duplicate(Decomposed body#1)	Decomposed body#16	4_M_DIEMAN,5,5
2@gl_k,121,197,0	duplicate(Decomposed body#1)	Decomposed body#17	4_M_DIEMAN,5,5
2@gl_k,107,194,0	duplicate(Decomposed body#1)	Decomposed body#18	4_M_DIEMAN,5,5
2@gl_k,92,187,0	duplicate(Decomposed body#1)	Decomposed body#19	4_M_DIEMAN,5,5
2@gl_k,153,214,3	duplicate(Decomposed body#1)	Decomposed body#20	4_M_DIEMAN,5,5
2@gl_k,155,195,4	duplicate(Decomposed body#1)	Decomposed body#21	4_M_DIEMAN,5,5
2@gl_k,154,188,2	duplicate(Decomposed body#1)	Decomposed body#22	4_M_DIEMAN,5,5
2@gl_k,143,195,5	duplicate(Decomposed body#1)	Decomposed body#23	4_M_DIEMAN,5,5
2@gl_k,132,214,4	duplicate(Decomposed body#1)	Decomposed body#24	4_M_DIEMAN,5,5
2@gl_k,125,208,2	duplicate(Decomposed body#1)	Decomposed body#25	4_M_DIEMAN,5,5
2@gl_k,114,210,3	duplicate(Decomposed body#1)	Decomposed body#26	4_M_DIEMAN,5,5
2@gl_k,137,182,6	duplicate(Decomposed body#1)	Decomposed body#27	4_M_DIEMAN,5,5
2@gl_k,138,246,0	duplicate(Decomposed body#1)	Decomposed body#28	4_M_DIEMAN,5,5
2@gl_k,132,260,7	duplicate(Decomposed body#1)	Decomposed body#29	4_M_DIEMAN,5,5
2@gl_k,128,251,1	duplicate(Decomposed body#1)	Decomposed body#30	4_M_DIEMAN,5,5
2@gl_k,179,260,3	duplicate(Decomposed body#1)	Decomposed body#31	4_M_DIEMAN,5,5
2@gl_k,170,261,4	duplicate(Decomposed body#1)	Decomposed body#32	4_M_DIEMAN,5,5
2@gl_k,177,219,2	duplicate(Decomposed body#1)	Decomposed body#33	4_M_DIEMAN,5,5
2@gl_k,190,214,5	duplicate(Decomposed body#1)	Decomposed body#34	4_M_DIEMAN,5,5
2@gl_k,201,214,4	duplicate(Decomposed body#1)	Decomposed body#35	4_M_DIEMAN,5,5

// Amdarais Spawn
2@gl_k,153,250,8	script	Heinrich#23	4_M_HEINRICH,{
	cutin "gl_heinrich1",2;
	mes "[Heinrich]";
	mes "เจ้าใส่อะไรฮิเมลเมซ!";
	close3;
}

2@gl_k,162,250,1	script	Varmundt#23	4_M_BARMUND,{
	mes "[Varmundt]";
	mes "ถอดบังเหียนนี้ไม่ได้ ...";
	cutin "gl_barmund2",2;
	close3;
}

2@gl_k,158,255,1	script	Gerhalt#23	4_LEVITATEMAN,{
	mes "[Gerhalt]";
	mes "อู้วววว ... ค๊า! เพียงแค่หนีไปพร้อมกับผู้บัญชาการ";
	close;
}

2@gl_k,158,252,1	script	Himelmez#22	4_F_HIMEL,3,3,{
	cutin "gl_himel2",2;
	mes "[Himelmez]";
	mes "มันวิเศษมาก ~ เจ้าทำมันมาถึงจุดนี้แล้ว ข้าขอขอบคุณได้ไหม";
	close3;

OnTouch:
	end;
OnTouch_:
	disablenpc instance_npcname("Himelmez#22");
	enablenpc instance_npcname("Himelmez#23");
	initnpctimer;
	'npc_himelmez$ = instance_npcname("Himelmez#23");
	'npc_gerhalt$ = instance_npcname("Gerhalt#23");
	'npc_heinrich$ = instance_npcname("Heinrich#23");
	'npc_varmundt$ = instance_npcname("Varmundt#23");
	end;
OnTimer3000:
	npctalk "Himelmez: สุดยอดมาก ~ จ้าคิดว่าเจ้าไม่ได้อยู่ใกล้ที่จะมาถึงที่นี่ด้วยซ้ำ ...", 'npc_himelmez$;
	end;
OnTimer6000:
	npctalk "Gerhalt: กร๊ากกก! หนี! ข้าไม่สามารถทนได้อีกต่อไป!", 'npc_gerhalt$;
	end;
OnTimer9000:
	npctalk "Heinrich: Gerhalt!", 'npc_heinrich$;
	end;
OnTimer15000:
	npctalk "Heinrich: เจ้ากำลังทำอะไรกับทหารของฉัน Himelmez!", 'npc_heinrich$;
	end;
OnTimer18000:
	npctalk "Himelmez: ฮูฮู ข้าพบชิ้นส่วนหัวใจของ Ymir แล้ว Heinrich", 'npc_himelmez$;
	end;
OnTimer21000:
	npctalk "Himelmez: ถ้าเขาไม่รบกวนข้า มันน่าจะเร็วกว่านี้", 'npc_himelmez$;
	end;
OnTimer24000:
	npctalk "Gerhalt: ผู้บัญชาการ ... มาเถอะนายต้องหนีไปจากที่นี่ ... ฮึ", 'npc_gerhalt$;
	end;
OnTimer27000:
	npctalk "Himelmez: จริงเหรอ? อะไรทำให้เจ้าคิดอย่างนั้น", 'npc_himelmez$;
	end;
OnTimer30000:
	npctalk "Heinrich: ฮิเมลเมซ! เจ้าได้ทำในสิ่งที่เจ้าต้องการแล้วจึงไม่จำเป็นต้องเสียสละอีกต่อไป!", 'npc_heinrich$;
	end;
OnTimer33000:
	npctalk "Heinrich: ปล่อยให้เขาไป! ข้าไม่ต้องเห็นใครต้องทนทุกข์!", 'npc_heinrich$;
	end;
OnTimer36000:
	npctalk "Himelmez: ปล่อยให้เขาไปเหรอ? ข้าคิดว่า ... ไม่ ...", 'npc_himelmez$;
	end;
OnTimer39000:
	npctalk "Himelmez: นอกจากนี้นี่เป็นทหารคนสุดท้ายของเจ้า Heinrich", 'npc_himelmez$;
	end;
OnTimer42000:
	npctalk "Himelmez: เขาทำให้ชีวิตข้ายากลำบาก", 'npc_himelmez$;
	end;
OnTimer45000:
	npctalk "Himelmez: เขาเหมาะอย่างยิ่งสำหรับ Amdarais ที่สร้างขึ้นใหม่ของข้า", 'npc_himelmez$;
	end;
OnTimer48000:
	npctalk "Heinrich: ข้าจะไม่มีวันให้อภัยเจ้า", 'npc_heinrich$;
	end;
OnTimer51000:
	npctalk "Himelmez: เอ่อเอ่อ ~ ข้ากลัวมาก.", 'npc_himelmez$;
	end;
OnTimer54000:
	npctalk "Himelmez: อย่างไรก็ตามเราจะมีโอกาสได้เห็นเพราะต้องคุยกันมากขึ้น", 'npc_himelmez$;
	end;
OnTimer57000:
	npctalk "Himelmez: เด็กชายยาวมาก", 'npc_himelmez$;
	end;
OnTimer60000:
	specialeffect EF_BARRIER, AREA, 'npc_gerhalt$;
	end;
OnTimer63000:
	disablenpc 'npc_himelmez$;
	end;
OnTimer65000:
	npctalk "Gerhalt: เจ้าสามารถสร้างร่างกายของข้าได้ แต่เจ้าไม่สามารถรับวิญญาณของข้าได้ฮิเมลเมซ!", 'npc_gerhalt$;
	end;
OnTimer66000:
	specialeffect EF_CHAINCOMBO, AREA, 'npc_gerhalt$;
	end;
OnTimer67000:
	npctalk "Heinrich: ข้าจะไม่มีวันปล่อยให้เจ้าหนีฮิเมลเมซ !!", 'npc_heinrich$;
	specialeffect EF_MAPPILLAR, AREA, 'npc_gerhalt$;
	end;
OnTimer70000:
	specialeffect EF_MAPPILLAR2, AREA, 'npc_gerhalt$;
	specialeffect EF_MAPPILLAR, AREA, 'npc_gerhalt$;
	disablenpc 'npc_heinrich$;
	npctalk "Varmundt: เราไม่มีทางเลือก เราต้องต่อสู้กับ Amdarais!", 'npc_varmundt$;
	end;
OnTimer73000:
	mapannounce 'map_name$[1], "ร่างกายของ Gerhalt กำลังเปลี่ยนไป", bc_map,0xFFFFFF;
	end;
OnTimer76000:
	specialeffect EF_LORD, AREA, 'npc_gerhalt$;
	disablenpc 'npc_varmundt$;
	mapannounce 'map_name$[1], "Varmundt: ให้ข้าช่วยการต่อสู้ครั้งนี้ด้วยภาพลวงตาของข้า เพียงทำตามคำสั่งของภาพลวงตา", bc_map,0xFFFF00;
	end;
OnTimer80000:
	disablenpc 'npc_gerhalt$;
	donpcevent instance_npcname("#ghmemorialmob07") + "::OnStart";
	stopnpctimer;
	'npc_himelmez$ = 'npc_gerhalt$ = 'npc_heinrich$ = 'npc_varmundt$ = "";
	end;
}

2@gl_k,158,252,4	script	Himelmez#23	4_F_HIMEL,{
	cutin "gl_himel2",2;
	mes "[Himelmez]";
	mes "มันวิเศษมาก ~ เจ้าทำมันมาถึงจุดนี้แล้ว ข้าขอขอบคุณได้ไหม";
	close3;
}

2@gl_k,1,1,0	script	#ghmemorialmob07	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob07");
	monster 'map_name$[1],158,255,"Amdarais",2476,1,instance_npcname("#ghmemorialmob07") + "::OnMyMobDead";// MG_AMDARAIS
	'boss_id = $@mobid[0];
	unittalk 'boss_id, "หนี ... หนีไปจากข้า ...";
	initnpctimer;
	end;
OnTimer5000:
	unittalk 'boss_id, "ข้าไม่ต้องการ ... ข้าไม่ต้องการฆ่าใคร อู้วววววววว";
	end;
OnTimer10000:
	unittalk 'boss_id, "ได้โปรด... ฆ่าข้าที";
	end;
OnTimer16000:
	unittalk 'boss_id, "เอ๊ยยยยยยยยยยยยยยยยย ... ตาย ... ตาย ...";
	end;
OnTimer22000:
	unittalk 'boss_id, "รื้อ ... ตาย! ...";
	end;
OnTimer55000:
	donpcevent instance_npcname("#ghmemorialmob08") + "::OnStart";// Varmundt buffs and additionnal monsters
	stopnpctimer;
	end;

OnMyMobDead:
	if (mobcount('map_name$[1], instance_npcname("#ghmemorialmob07") + "::OnMyMobDead") < 1) {
		stopnpctimer;
		enablenpc instance_npcname("Hugin#21");
		enablenpc instance_npcname("#Secret Room Exit");

		stopnpctimer;
		disablenpc instance_npcname("#ghmemorialmob07");
		// note: monsters from #ghmemorialmob08 still alive on Amdarais's dead
	}
	end;
}

// Varmundt Buffs
2@gl_k,1,1,0	script	#ghmemorialmob08	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob08");
	initnpctimer;
	end;
OnTimer5000:
	if (unitexists('boss_id) == 0) {
		disablenpc instance_npcname("#ghmemorialmob08");
		stopnpctimer;
		end;
	}
	getunitdata 'boss_id, .@data;
	.@percent_hp = (.@data[UMOB_HP] * 100) / .@data[UMOB_MAXHP];
	mapannounce 'map_name$[1], "Amdarais HP " + .@percent_hp + "% reach!", bc_map,0x70DBDB;

	// event type every 10%
	switch( .@percent_hp / 10 ) {
	case 10:
		break;
	case 9:
		donpcevent instance_npcname("Varmundt's Ghost#Buff2") + "::OnEvent";
		break;
	case 8:
		donpcevent instance_npcname("Varmundt's Ghost#Buff3") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"Suffered Khalitzburg",2471,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	case 7:
		donpcevent instance_npcname("Varmundt's Ghost#Buff3") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"Abysmal Knight",2470,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	case 6:
		donpcevent instance_npcname("Varmundt's Ghost#Buff3") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"Bloody Knight",2472,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	case 5:
		donpcevent instance_npcname("Varmundt's Ghost#Buff2") + "::OnEvent";
		break;
	case 4:
		donpcevent instance_npcname("Varmundt's Ghost#Buff3") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"Wandering Archer",2469,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	case 3:
		donpcevent instance_npcname("Varmundt's Ghost#Buff1") + "::OnEvent";
		donpcevent instance_npcname("Varmundt's Ghost#Buff3") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"Bloody Knight",2472,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	case 2:
		donpcevent instance_npcname("Varmundt's Ghost#Buff1") + "::OnEvent";
		donpcevent instance_npcname("Varmundt's Ghost#Buff3") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"Abysmal Knight",2470,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	case 1:
	case 0:
		donpcevent instance_npcname("Varmundt's Ghost#Buff1") + "::OnEvent";
		donpcevent instance_npcname("Varmundt's Ghost#Buff4") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"Wandering Archer",2469,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	}
	end;
OnTimer30000:
	killmonster 'map_name$[1], instance_npcname("#ghmemorialmob08") + "::OnMobDead";
	initnpctimer;
	end;
OnMobDead:
	end;
}

2@gl_k,150,247,5	script	Varmundt's Ghost#Buff1	4_M_BARMUND,2,2,{
	end;
OnTouch:
	specialeffect2 EF_ENHANCE;
	.@num = atoi( replacestr(strnpcinfo(2), "Buff", "") );
	if (.@num == 1)
		sc_start SC_GLASTHEIM_HIDDEN,30000,1,10000,SCSTART_NOTICKDEF;
	else if (.@num == 2)
		sc_start SC_GLASTHEIM_STATE,30000,1,10000,SCSTART_NOTICKDEF;
	else if (.@num == 3) {
		sc_start SC_GLASTHEIM_HEAL,30000,1,10000,SCSTART_NOTICKDEF;
		sc_start SC_GLASTHEIM_HPSP,30000,1,10000,SCSTART_NOTICKDEF;
	}
	else {
		sc_start SC_GLASTHEIM_DEF,30000,1,10000,SCSTART_NOTICKDEF;
		sc_start SC_GLASTHEIM_ITEMDEF,30000,1,10000,SCSTART_NOTICKDEF;
	}
	end;
OnEvent:
	initnpctimer;
	enablenpc instance_npcname( strnpcinfo(0) );
	.@num = atoi( replacestr(strnpcinfo(2), "Buff", "") );
	if (.@num == 1)
		npctalk "Varmundt's Ghost: Amdarais อาจใช้โล่เวทมนตร์ที่แข็งแกร่ง! อยู่ใกล้ๆข้า แล้วข้าจะปกป้องท่านเอง!";
	else if (.@num == 2)
		npctalk "Varmundt's Ghost: ตอนนี้เป็นโอกาสที่จะโจมตี! เข้ามาใกล้ข้าแล้วฟาด!";
	else if (.@num == 3)
		npctalk "Varmundt's Ghost: หากท่านไม่ต้องการถูกโจมตีโดยซอมบี้ของ Amdarais มาหาข้าและรับพลังมากขึ้น!";
	else
		npctalk "Varmundt's Ghost: หากท่านไม่ต้องการถูกโจมตีด้วยพลังของ Amdarais มาหาข้าและรับพลังเพิ่ม!";
	end;
OnTimer10000:
	stopnpctimer;
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}
2@gl_k,165,247,3	duplicate(Varmundt's Ghost#Buff1)	Varmundt's Ghost#Buff2	4_M_BARMUND,2,2
2@gl_k,150,232,8	duplicate(Varmundt's Ghost#Buff1)	Varmundt's Ghost#Buff3	4_M_BARMUND,2,2
2@gl_k,165,232,2	duplicate(Varmundt's Ghost#Buff1)	Varmundt's Ghost#Buff4	4_M_BARMUND,2,2


2@gl_k,158,241,1	script	Hugin#21	4_M_SAGE_C,{
	if (checkquest(12319,HUNTING) == 2) {// note: complete hunting 'Corrupted Soul Hunt' prerequis
		mes "[Hugin]";
		mes "อืม ท่านสบายดีมาก ก่อนอื่นให้ข้ามอบของขวัญจาก Amdarais ให้ท่าน";
		erasequest 12319;
		setquest 12322;// Space Distortion
		if (isbegin_quest(12321) == 0) {
			setquest 12321;// Time Conqueror
			completequest 12321;
			getitem 6607,5;// Temporal_Crystal
			getitem 6608,5;// Coagulated_Spell
			getexp 350000,350000;
		}
		else {
			getitem 6607,1;// Temporal_Crystal
			getitem 6608,1;// Coagulated_Spell
		}
		next;
		mes "[Hugin]";
		mes "ข้าหยุดเวลาของ Varmundt";
		mes "และเวลาของท่านจะผิดเพี้ยนไปในไม่ช้า";
		specialeffect2 EF_BLIND;
		soundeffect "_blind.wav",0;
		next;
		mes "[Hugin]";
		mes "บางทีนักเดินทางผู้น่าสงสารคนนี้อาจพยายามหยุดโศกนาฏกรรมของ Glast Heim ไม่ให้เกิดขึ้น";
		next;
		mes "[Hugin]";
		mes "อย่างไรก็ตามเราจะไม่มองข้ามพฤติกรรมของเขาในตอนนี้และตลอดไป ...";
		next;
		mes "[Hugin]";
		mes "ตอนนี้ให้ข้าลบความทรงจำของท่าน ถ้าท่านเจอข้าอีกครั้งมันจะเป็นคนใหม่";
		specialeffect2 EF_FREEZE;
		close2;
		warp 'map_name$[1],158,244;
		end;
	}
	mes "[Hugin]";
	mes "อ๊ะ. ฉันแทบจะบิดเบือนช่องว่างของเวลา มานี้สิ. เราต้องออกไป!";
	next;
	select("ท่านแค่อยู่กับข้า ...");
	mes "[Hugin]";
	mes "เมื่อกี้ท่านพูดอะไร?";
	mes "อย่างไรก็ตามนั่นไม่สำคัญ ช่องว่างของเวลาจะถูกปิดดังนั้นเราต้องออกไปจากที่นี่";
	next;
	if (select("อยู่ต่อที่นี้: ออกจากที่นี้") == 1) {
		mes "[Hugin]";
		mes "จริงๆ? สถานที่แห่งนี้จะถูกทำลายลงในไม่ช้า โปรดมองไปรอบ ๆ อย่างรวดเร็ว";
		close;
	}
	close2;
	warp "glast_01",204,270;
	end;
}

// Treasure Room
//============================================================
1@gl_k,165,136,3	script	Strange crack#2	CLEAR_NPC,{
	if (isbegin_quest(12322) == 0) {// Space Distortion
		mes "รอยแตกดูน่าสงสัย แต่ไม่มีอะไรให้ตรวจสอบอีกต่อไป";
		close;
	}
	specialeffect EF_SPELLBREAKER;

	.@random = rand(1,4);
	switch( atoi(strnpcinfo(2)) ) {
	case 2:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 719,1,"this",165,138;
		makeitem 6608,1,"this",165,138;
		break;
	case 3:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 720,1,"this",159,138;
		if (rand(1,4) == 4)
			makeitem2 15066,1,"this",159,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",159,138;
		makeitem 6607,1,"this",159,138;
		break;
	case 4:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 721,1,"this",153,138;
		if (rand(1,4) == 4)
			makeitem2 13086,1,"this",153,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",153,138;
		makeitem 6607,1,"this",153,138;
		break;
	case 5:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 6608,1,"this",147,138;
		if (rand(1,4) == 4)
			makeitem2 2949,1,"this",147,138,0,0,0,0,0,0,0;
		makeitem 6607,1,"this",147,138;
		makeitem 6607,1,"this",147,138;
		makeitem 6608,1,"this",147,138;
		break;
	case 6:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 6607,1,"this",141,138;
		makeitem 6608,1,"this",141,138;
		if (rand(1,4) == 4)
			makeitem2 13440,1,"this",141,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",141,138;
		break;
	case 7:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 6607,1,"this",135,138;
		if (rand(1,4) == 4)
			makeitem2 6608,1,"this",135,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",135,138;
		break;
	case 8:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 6608,1,"this",129,138;
		if (rand(1,4) == 4)
			makeitem2 21007,1,"this",129,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",129,138;
		break;
	}
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}
1@gl_k,159,136,3	duplicate(Strange crack#2)	Strange crack#3	CLEAR_NPC
1@gl_k,153,136,3	duplicate(Strange crack#2)	Strange crack#4	CLEAR_NPC
1@gl_k,147,136,3	duplicate(Strange crack#2)	Strange crack#5	CLEAR_NPC
1@gl_k,141,136,3	duplicate(Strange crack#2)	Strange crack#6	CLEAR_NPC
1@gl_k,135,136,3	duplicate(Strange crack#2)	Strange crack#7	CLEAR_NPC
1@gl_k,129,136,3	duplicate(Strange crack#2)	Strange crack#8	CLEAR_NPC

1@gl_k,269,267,3	script	Strange crack#1	CLEAR_NPC,{
	if (isbegin_quest(12322) == 0) {// Space Distortion
		mes "รอยแตกดูน่าสงสัย แต่ไม่มีอะไรให้ตรวจสอบอีกต่อไป";
		close;
	}
	warp 'map_name$[0],149,198;
	end;

OnInstanceInit:
	'map_name$[0] = instance_mapname("1@gl_k");
	'map_name$[1] = instance_mapname("2@gl_k");

	// Entrance
	disablenpc instance_npcname("Himelmez#1");
	disablenpc instance_npcname("Varmundt#1");
	disablenpc instance_npcname("Heinrich#1");
	disablenpc instance_npcname("Heinrich#2");
	disablenpc instance_npcname("Heinrich#3");

	// Rescue 1
	disablenpc instance_npcname("#ghmemorialmob01");
	disablenpc instance_npcname("Altar boy Domun#1");

	// Rescue 2
	disablenpc instance_npcname("#ghmemorialmob02");
	disablenpc instance_npcname("Holgren the Destroyer#1");
	for ( .@i = 1; .@i <= 26; .@i++ )
		disablenpc instance_npcname("A dead man#" + .@i);

	// Sector 3
	disablenpc instance_npcname("#ghmemorialmob03");

	// Root of Corruption
	disablenpc instance_npcname("Himelmez#2");
	disablenpc instance_npcname("Varmundt#2");
	disablenpc instance_npcname("Heinrich#4");
	disablenpc instance_npcname("#Mimelon");
	disablenpc instance_npcname("#ghmemorialmob04");

	// Entrance 2nd map
	disablenpc instance_npcname("Varmundt#21");
	disablenpc instance_npcname("Heinrich#21");
	disablenpc instance_npcname("#Servanton");
	hideonnpc instance_npcname("#Servanton_effect");

	// Commanders
	disablenpc instance_npcname("#ghmemorialmob05");
	disablenpc instance_npcname("#ghmemorialmob06");
	for ( .@i = 1; .@i <= 7; .@i++ ) {
		disablenpc instance_npcname("#ogh_2-" + .@i);
		disablenpc instance_npcname("#ogh_3-" + .@i);
	}

	// Amdarais Room Entrance
	disablenpc instance_npcname("#Geron");
	for ( .@i = 1; .@i <= 35; .@i++ )
		disablenpc instance_npcname("Decomposed body#" + .@i);

	// Amdarais Spawn
	disablenpc instance_npcname("Gerhalt#23");
	disablenpc instance_npcname("Himelmez#22");
	disablenpc instance_npcname("Himelmez#23");
	disablenpc instance_npcname("Varmundt#23");
	disablenpc instance_npcname("Heinrich#23");
	disablenpc instance_npcname("#ghmemorialmob07");

	// Varmundt Buffs
	disablenpc instance_npcname("#ghmemorialmob08");
	disablenpc instance_npcname("Varmundt's Ghost#Buff1");
	disablenpc instance_npcname("Varmundt's Ghost#Buff2");
	disablenpc instance_npcname("Varmundt's Ghost#Buff3");
	disablenpc instance_npcname("Varmundt's Ghost#Buff4");
	disablenpc instance_npcname("Hugin#21");

	// Warps
	disablenpc instance_npcname("#2Control");
	disablenpc instance_npcname("#2Control2");
	disablenpc instance_npcname("#3Control");
	disablenpc instance_npcname("#3Control2");
	disablenpc instance_npcname("#4Control");
	disablenpc instance_npcname("#4Control2");
	disablenpc instance_npcname("#2F Entrance");
	disablenpc instance_npcname("#1 Control");
	disablenpc instance_npcname("#22 Control");
	disablenpc instance_npcname("#22 Control2");
	disablenpc instance_npcname("#23 Control");
	disablenpc instance_npcname("#23 Control2");
	disablenpc instance_npcname("#24 Control");
	disablenpc instance_npcname("#24 Control2");
	disablenpc instance_npcname("#Secret Room Exit");
	end;

}