//=======================================================================================================//
//						Doll Quest
//=======================================================================================================//
alberta,117,135,0	script	Elin	96,{
	if (checkweight(1201,1) == 0) {
		mes "[Elin]";
		mes "^3355FFWait a second! Right now, you're carrying too many items with you. Please come back after putting some of your things into Kafra Storage.^000000";
		close;
	}
	mes "[Elin]";
	if (Sex == 1) {
		mes "วีายๆ หล่อจังเลย~!";
		mes "ว่างัยจ๊ะ.. คุณผู้ชายคงเป็นปกติที่ไม่ชอบเล่นตุ๊กตาใช่ไหม? แต่ฉันน่ะชอบเล่นตุ๊กตามาก ๆ คุณต้องการตุ๊กตาไหม?";
	} else {
		mes "ฮิ ฮุ ฮิ~! เธอต้องการตุ๊กตาหรือไม่? ฮ่า ฮ่า ฮ่า ฉันชอบตุ๊กตา!";
	}
	next;
	mes "[Elin]";
	mes "คุณรู้  ฉันต้องการตุ๊กตาตัวใหม่ ๆ! ฉันน่ะอยากได้ตุ๊กตาตัวใหม่ในวันเกิดของฉัน...!";
	next;
	switch(select("อืม ฉันจะให้หนึ่งตัวชอบคุ๊กตาอะไร.","ฉันจะให้คุณยังไง?")) {
		case 1:
			mes "[Elin]";
			mes "หรือ..จริง ๆ น่ะ";
			mes "ฉันอยากได้ตุ๊กตา Yoyo";
			mes "อยากได้ อยากได้..                                                                                                                                                                                                                                                                 !";
			close;
		case 2:
			set .@weight,MaxWeight-Weight;
			if ((.@weight) < 1000) {
				mes "[Elin]";
				if (Sex == 1) {
					mes "ขออภัยน่ะจ๊ะพี่ชาย พี่ชายไม่สามารถทำรายการนี้ได้จ๊ะ เนื่องจากของเต็มตัวเลยจ๊ะ เอาของออกไปฝากที่ คลังเก็บของก่อน..แล้วค่อยกลับมาจร้า~";
				} else {
					mes "หนูคงให้พี่สาวผ่านการตรวจสอบไม่ได้ เพราะว่าของเยอะเกินไปไปเก็บของไว้ที่คลังเก็บของซะก่อน..คงเขาใจหนูน่ะ";
				}
				close;
			}
			mes "[Elin]";
			if (Sex == 1) {
				mes "โอ้ พระเจ้า ช่วย..!";
				mes "ทำได้จริง ๆ ? พี่ชายนี่เก่งจริง";
				mes "น่ารักจังเลยพี่ชาย~!";
			} else {
				mes "พี่สาวเก่งจังเลย?";
				mes "หนูขอตุ๊กตาน่ะ..";
				mes "เย้ เย้ เย้...";
			}
			next;
			mes "[Elin]";
			mes "ตุ๊กตาตัวไหนที่พี่ชายพี่สาวจะมอบให้หนู? ";
			next;
			switch(select("Poring Doll","Chonchon Doll","Puppet","Rocker Doll","Spore Doll","Osiris Doll","Baphomet Doll","Raccoon Doll","Yoyo Doll","ไม่สนใจ")) {
				case 1:
					if (countitem(741) >= 1) {
						delitem 741,1;	// Poring_Doll
						mes "[Elin]";
						mes "อิอิ...";
						mes "ฉันมีจำนวนมาก";
						mes "สำหรับ Poring dolls...";
						next;
						mes "[Elin]";
						if (Sex == 1) {
							mes "แต่หนูเป็นคนชอบเก็บสะสมตุ๊กตาทุกตัว  Poring doll ก็น่ารักดีน่ะค่ะพี่ชาย~";	
						} else {
							mes "แต่ ก็ขอบคุณพี่สาวที่มอบ Poring doll ให้กับหนู~";
						}
						next;
						mes "[Elin]";
						mes "เอ้อ หนูมีสิ่งของเล็ก ๆ น้อย ๆ ที่แทนคำขอบคุณจากพี่ ๆ ที่มอบตุ๊กตาให้หนู";
						next;
						getitem 529,1;	// Candy
						mes "[Elin]";
						if (Sex == 1) {
							mes "หนูเข้าใจน่ะค่ะว่าพี่ชายคงไม่ชอบ Candy แต่หนูคิดว่ายี่ห้อนี้อร่อยมาก ๆ เลยน่ะค่ะ";
						} else {
							mes "หนูคิดว่าพี่สาวอาจจะชอบ ลูกอมยี่ห้อนี้? รสชาติของมันหวานมากมายเลย";
						}
						next;
						mes "[Elin]";
						mes "ขอบคุณน่ะจ๊ะ";
						mes "สำหรับพี่ ๆ ที่มอบ";
						mes "Poring doll ให้หนู!";
						close;
					} else {
						mes "[Elin]";
						if (Sex == 1) {
							mes "หือ....?";
							mes "อะไรของพี่ชายเนี่ย..ตุ๊กตาก็ไม่ยอมพกมา ";
						} else {
							mes "หือ...";
							mes "พี่สาวกำลังจะเล่นตลกกับหนูอยู่หรองัยค่ะ ?";
						}
						next;
						mes "[Elin]";
						mes "หนูคิดว่าพี่ ๆ คงจะลืมตุ๊กตาที่จะให้หนูน่ะ...";
						close;
					}
				case 2:
					if (countitem(742) >= 1) {
						delitem 742,1;	// Chonchon_Doll
						mes "[Elin]";
						mes "ยี้... --!";
						mes "คิดว่าแมลงวันซะอีก  ที่แท้ก็ตุ๊กตา Chonchon!";
						next;
						mes "[Elin]";
						if (Sex == 1) {
							mes "แต่..คนทำตุ๊กตานี้ทำเหมือนแมลงวันจริงเลย แต่น่ารักดี ขอบคุณค่ะพี่ชาย";
						} else {
							mes "แต่ ตุ๊กตาตัวนี้เหมือนของขวัญที่ดีสำหรับของหนูเลยน่ะค่ะ ขอบคุณค่ะ";
						}
						next;
						mes "[Elin]";
						mes "เอ้อ~";
						mes "หนูมีของจะให้ค่ะ";
						mes "รอแปบหนึ่งน่ะจ๊ะ..."	;
						next;
						getitem 530,1;	// Candy_Striper
						mes "[Elin]";
						mes "ลูกอมนี้สั่งมาจาก เมือง Prontera เป็นยี่ห้อดังของเมืองนั้นเลย แทนคำขอบคุณ";
						next;
						mes "[Elin]";
						mes "และ..";
						mes "หนู...";
						mes "ขอบคุณพี่มาก ๆ";
						mes "ที่มอบตุ๊กตา...";
						close;
					} else {
						mes "[Elin]";
						mes "ตุ๊กตา Chonchon ไม่น่ารักก็จริง แต่ฉันก็ชอบสะสม ดูดี ๆ ไม่ก็สวยจริงไหม ?!";
						next;
						mes "[Elin]";
						mes "พี่หาให้หนูได้แล้วค่อยกลับมาน่ะ..~~";
						close;
					}
				case 3:
					if (countitem(740) >= 1) {
						delitem 740,1;	// Stuffed_Doll
						mes "[Elin]";
						mes "ว้าว...!";
						mes "น่ารักจ๊าง.. bunny!";
						next;
						mes "[Elin]";
						mes "มันเป็นตูกตากระต่ายที่น่ารักจริง ๆ~ ขอบคุณมาก ๆ~";
						next;
						mes "[Elin]";
						mes "โอ้..เอ๋!";
						mes "ฉันมีบางสิ่งจะ..";
						mes "มอบให้พี่ ๆ..!";
						mes "อืม, มันอยู่ที่ไหนน่ะ";
						mes "อ่ะฮ่ะ เจอแล้ว...";
						next;
						getitem 530,1;	// Candy_Striper
						mes "[Elin]";
						mes "อ่ะอันนี้เลย! มันเป็นลูกอมที่มีแต่เทศกาล. รับไว้เถอะมันเป็นของที่หายากมาเลย แลกกับตุ๊กตาที่มอบให้...";
						close;
					} else {
						mes "[Elin]";
						mes "หือ.. พี่คิดจะแกล้งหนูหรือไง? ทำไมไม่นำตุ๊กตามาให้หนู?";
						next;
						mes "[Elin]";
						mes "^666666*หนูโกธรจริง ๆ ด้วย*^000000";
						next;
						mes "[Elin]";
						mes "แม่จ๊ะ...มีพี่ ๆ ใจร้ายแกล้งหนู~~";
						close;
					}
				case 4:
					if (countitem(752) >= 1) {
						delitem 752,1;	// Grasshopper_Doll
						mes "[Elin]";
						mes "โอ้! ตุ๊กตา Rocker!";
						mes "ฉันเห็นตุ๊กตาตัวนี้เหมือนมีชีวิต! แต่ฉันไม่ชอบขาของมัน, แต่โดยรวมก็ โอเค~ เพราะฉันชอบสะสมของแปลก";
						next;
						mes "[Elin]";
						mes "เอ๋, ฉันมีของบางอย่างจะให้พี่? รอสักครู่น่ะเดียวได้เห็นของหนูบ้าง...";
						next;
						getitem 532,7;	// Banana_Juice
						mes "[Elin]";
						mes "นำมาไปเลย! น้ำกล้วยหอม ท่านชอบไหม..? สำหรับตุ๊กตาตัวนี้..!";
						close;
					} else {
						mes "[Elin]";
						mes "อะไรอ่ะ...";
						next;
						mes "[Elin]";
						mes "คุณไม่มี..";
						mes "ตุ๊กตาซักตัว จริงๆหรือ...?";
						mes "แล้วจะมาที่นี้ทำไมละ...";
						close;
					}
				case 5:
					if (countitem(743) >= 1) {
						delitem 743,1;	// Spore_Doll
						mes "[Elin]";
						mes "เห็ดนี่คืออะไรหรอ...?";
						mes "เอ๋.., นี่ตุ๊กตาหรือ!";
						mes "เห็นแล้วนึกว่า..";
						mes "เห็ดเกือบนำไปทำเป็นอาหารซะแล้ว...";
						next;
						mes "[Elin]";
						mes "แต่ตุ๊กตานี้ออกแบบมาดีจริง ๆฉันชอบมากเลย~";
						next;
						getitem 538,5;	// Well_Baked_Cookie
						mes "[Elin]";
						mes "เอานี้ไปเลยจร้า. ฉันชอบทำอาหารกินทุกวัน..เลยจะแบ่งอาหารให้พี่ใช้ในการเดินทาง!";
						next;
						mes "[Elin]";
						mes "ขอบคุณน่ะค่ะ";
						mes "ที่ให้ตุ๊กตาตัวนี้..";
						mes "ฉันจะดูแลมันอย่างดีเลย!";
						close;
					} else {
						mes "[Elin]";
						mes "ใจร้าย...?";
						mes "พี่ไม่ยอมหาตุ๊กตาให้";
						mes "แม้ซักตัวก็ไม่มีเลย";
						next;
						mes "[Elin]";
						mes "......";
						mes "หนูงอลพี่แล้ว..";
						mes "ไปเลยหนูไม่อยากเจอพี่อีก";
						mes "คนผิด ^666666*สัญญา*^000000";
						close;
					}
				case 6:
					if (countitem(751) >= 1) {
						delitem 751,1;	// Osiris_Doll
						mes "[Elin]";
						mes "น่ากลัวจังเลย!";
						mes "นี่มันคืออะไรค่ะพี่..";
						mes "หนูไม่กล้าเข้าใกล้มันเลย!";
						next;
						mes "[Elin]";
						mes "นี่อตุ๊กตาหรือแล้วไป... ไปหามาจากไหนหรอค่ะ? แต่ดูไปแล้วน่ากลัวดีจริง ๆเอาไปอวดกับเพื่อน ดีกว่า คิคิ!";
						next;
						mes "[Elin]";
						mes "พี่หนูตุ๊กตาตัวนี้ได้ไหมค่ะ, หนูมีของดี ๆ หนูขอแลกกับพี่น่ะค่ะ.";
						next;
						getitem 522,2;	// Fruit_Of_Mastela
						mes "[Elin]";
						mes "รอสักครู่ฉันกับสิ่งนี้อยู่ในที่ปลอดภัย. รับลองว่าพี่ต้องชอบมันแลกกับหนูน่ะ!";
						if (Sex == 1) {
							mes "ของชิ้นนี้พี่สามารถนำไปให้กับ ผู้หญิงที่รัก.. ได้รับลองของดีแน่ ๆ~!";
						} else {
							mes "ของชิ้นนี้พี่สามารถนำไปให้กับ ผู้ชายในฝัน.. ได้รับลองของดีแน่ ๆ~!";
						}
						close;
					} else {
						mes "[Elin]";
						mes "ไปซะเลย...";
						mes "คุณไม่มีตุ๊กตาเลย";
						mes "นี่กำลังจะเล่นตลกกับฉันหรอ..?";
						close;
					}
				case 7:
					if (countitem(750) >= 1) {
						delitem 750,1;	// Baphomet_Doll
						mes "[Elin]";
						mes "ตัวอารายเนี่ย..?";
						mes "ตัวเล็ก ๆ มีเขาเหมือน ... เลย";
						next;
						mes "[Elin]";
						mes "ขอมองใกล้ ๆ หน่อย. อุ๊ยตุ๊กตาตัวนี้ทำไมถึงน่าร๊าก..ขนาดนี้ แลกกันน่ะ ได้หนูมีของจะมอบให้..";
						next;
						getitem 525,5;	// Panacea
						mes "[Elin]";
						mes "ของสิ่งนี้จริง ๆ หนูไม่อยากให้เท่าไหร่. มันเอาไว้ใช้ในยามเราติดพิษจากแมลงหรือสัตว์เลื้อยคาน~";
						next;
						mes "[Elin]";
						mes "แต่ว่าน่ะ~";
						mes "ขอบคุณสำหรับ";
						mes "ตุ๊กตาตัวนี้....!";
						close;
					} else {
						mes "[Elin]";
						mes "อะไรหว่า..! ไม่มีตุ๊กตาแล้วมาที่นี่ทำไมอ่ะ?!";
						close;
					}
				case 8:
					if (countitem(754) >= 1) {
						delitem 754,1;	// Raccoondog_Doll
						mes "[Elin]";
						mes "อุ๋ยย๋า.., นี่อะไรหรอ";
						mes "ตุ๊กตา raccoon.";
						mes "มันน่ารักสุดยอดเลย..~";
						next;
						mes "[Elin]";
						mes "ฉันไม่ชอบ...";
						mes "แต่ มันเป็นแค่ตุ๊กตา..ฉันมีไรจะให้!";
						next;
						getitem 539,3;	// Piece_Of_Cake
						mes "[Elin]";
						mes "อ่ะนี่คือเค้กที่ย่าฉันทำให้. เป็นสูตรพิเศษที่เราที่เดียว..เค้กเรา หวาน นุ่ม น่ากินมาก!";
						next;
						mes "[Elin]";
						mes "แต่ว่าน่ะหนู~";
						mes "ขอบคุณสำหรับตุ๊กตา...";
						mes "ฉันจะเก็บรักษา..";
						mes "ไว้ในห้องของฉันเลย!";
						close;
					} else {
						mes "[Elin]";
						mes "นี่ นี่! ทำไมพี่ถึงมาทำกับหนูอย่างงี้ละ? ไม่เห็นจะสนุกด้วยเลย..?!";
						close;
					}
				case 9:
					if (countitem(753) >= 1) {
						delitem 753,1;	// Monkey_Doll
						mes "[Elin]";
						mes "ว๊าาาาาาาาาว....~!";
						next;
						mes "[Elin]";
						mes "ตุ๊กตาลิงน้อย..! มันน่าร๊ากจ๊าง! พี่จะให้หนูใช้ไหม?! เย้! ขอบคุณพี่มากๆเลย! หนูชอบตุ๊กตาแล้วชอบพี่จังเลย!";
						next;
						mes "[Elin]";
						mes "เมื่อพี่มอบตุ๊กตาให้หนู";
						mes "ตุ๊กตาที่น่ารักอย่างงี้,";
						mes "หนูมีของจะให้พี่จ๊ะ";
						mes "โปรดรอหนูสักครู่!";
						next;
						getitem 608,1;	// Seed_Of_Yggdrasil
						mes "[Elin]";
						mes "สิ่งของชิ้นนี้ไม่สามารถหาได้ที่เมือง. หนูใช้เวลาค้นหามันมาหลายเดือน. กว่าจะได้แต่ละชิ้นเหนื่อยหมาย และทะเลาะกับทางบ้านตลอด. คุณคงไม่ต้องที่จะเหนื่ยเหมือนกับฉันหรอก?";
						next;
						mes "[Elin]";
						mes "อิอิ.....~";
						mes "ขอบคุณค่ะสำหรับ";
						mes "ตุ๊กตาตัวนี้..หนูจะกอดมัน";
						mes "หนูไปไหนจะพกมันติดตัวไปด้วย";
						mes "เวลาหลับก็จะนำมากอดข้างแขน";
						close;
					} else {
						mes "[Elin]";
						mes "นี่ นี่...";
						mes "อย่ามาตลกกับหนูน่ะค่ะพี่เดียวหาว่าไม่เตือน?!";
						close;
					}
				case 10:
					mes "["+strcharinfo(0)+"]";
					mes "พี่ไม่คิดชอบสะสมของบ้างหรืองัย...";
					next;
					mes "[Elin]";
					mes "คนอะไรก็ไม่รู้หาว่าไร้สาระ...?";
					next;
					mes "[Elin]";
					mes "^3355FFพี่มันไม่มี^000000";
					mes "^3355FFงานอดิเรกเอาซะเลย !^000000";
					close;
			}
	}
}


//=======================================================================================================//
//						Boy's Cap' Quest
//=======================================================================================================//
alberta_in,28,145,4	script	Grampa	120,{
	mes "[Grampa]";
	mes "*ว้าว.หรือ*...";
	mes "....*โอ้คุณพระ!*";
	mes "วิ๊วิ๊ว...";
	mes "บางคนอาจจะบอกได้ว่าการใช้ชีวิตที่เต็มรูปแบบกับผู้หญิงเป็นยังไง...";
	next;
	mes "[Grampa]";
	mes "แต่ฉันปัจจุบันก็ยังไม่มีผู้หญิงที่ต้องการเลย...หรือว่าเป็นเพราะรสนิยมของฉันมีนแย่... ผู้หญิงถึงไม่เหลียวมอง~*";
	next;
	if (countitem(1030) > 9) {
		switch(select("แสดง Tiger's Footskin:แลกหมวก Boys Cap:ยกเลิก")) {
		case 1:
			mes "[Grampa]";
			mes "ว้าว...~ !!";
			mes "ของชิ้นนี้คือ...?";
			mes "มันคือ..! ";
			mes "ตีน Tiger's Footskin!! ท่านไปหามาจากไหน!";
			next;
			mes "[Grampa]";
			mes "ท่านนี่ก็มีความสามารถเหมือนกันน่ะนี่.., แถมไปล่าเสือโดยไม่มีเลือดเลยทำอย่างกับไปฆ่าแมว";
			next;
			mes "[Grampa]";
			mes "ทำได้ดีจริง ๆ ...";
			mes "งั้นข้าขอ..!!!! The Tiger's Footskin ได้ไหม~!! ถ้าได้น่ะ! พระเจ้า !! ได้โปรด!!! โปรดมอบให้ข้าเถอะ... น่ะ...";
			close;
		case 2:
			delitem 1030,10;  //Tiger_Footskin
			getitem 5016,1; //Boy's_Cap
			mes "[Grampa]";
			mes "ข... ขอบคุ๊ณ !!";
			mes "งั้นข้า!! ขอมอบหมวกนี้ให้ท่านน่ะ.. !";
			next;
			mes "[Grampa]";
			mes ".............";
			mes "มันเป็นหมวกที่ดูดีมาก ๆ.";
			mes ".............";
			close;
		case 3:
			mes "[Grampa]";
			mes "นี่คุณ..., เฮ้ย !! ฟังฉันพูดก่อนดิ !";
			close;
		}
	} 
	else {
		switch(select("พูดคุย:ยกเลิก")) {
		case 1:
			mes "[Grampa]";
			mes "จะเล่าให้ฟังว่า...";
			mes "ฉันสามารถแก้ไขปัญหาที่คุณเผชิญอยู่ตอนนี้.. การที่คุณไม่มีผู้หญิงนั้น อย่างแรกคุณต้องมีบุคลิกภาพที่ดีก่อน";
			next;
			mes "[Grampa]";
			mes "อย่างแรกคือการวางตัวของเรานั้นดีไหม อันดับต่อไปนั้นคือการแต่งกายของท่าน..ว่าดีดูไหม";
			next;
			mes "[Grampa]";
			mes "หากคุณเปรียบผู้หญิงคือดอกไม้ที่สวยงามนั้น. คุณก็ทำให้เขาเห็นว่าเป็นผู้ชายที่เขาจะทิ้งที่ชีวิตไว้กับคุณได้ แต่ดิจากตัวคุณแล้ว มีอย่างเดียว";
			next;
			mes "[Grampa]";
			mes "ที่คุณยังขาดสิ่งที่ดี..?! นั้นคือคุณควรมีหมวกเท่ ๆ แต่ฉันให้ฟรี ๆ ต้องไปหา ^3355FFTiger's Footskin^000000 10 ชิ้น !";
			next;
			mes "[Grampa]";
			mes "แล้วคุณจะสามารถทำหมวก ^3355FFBoys Cap^000000";
			mes "หากหา 10 Tiger's Footskin ไม่ได้ ก็อย่าได้หมวกนี้เลย";
			close;
		case 2:
			mes "[Grampa]";
			mes "มันเสือน่ะ ....ไม่ใช้แมว .....เดียวมันจับเรา .....ไปทำหนังคน.....แล้วจะว่ายังไง ";
			close;
		}
	}
}

//=======================================================================================================//
//						Antlers Quest 
//=======================================================================================================//
alberta_in,122,53,4	script	Cherokee	47,{
	mes "[Cherokee]";
	mes "น๊า-นี่..~!";
	next;
	mes "[Cherokee]";
	mes "คุณคิดยังไงกับเขาสัตว์? แต่ฉันคิดได้ดีกว่าคุณ..? ฉันชอบเขาสัตว์เพราะว่าฉันชอบ ^3355FFสะสม เขาสัตว์^000000.";
	next;
	mes "[Cherokee]";
	mes "และฉันคิดว่าบางทีอาจจะชอบเขาสัตว์ก็เป็นได้ เขาประเภทม้านั้นเป็นเขาที่สง่างามที่สุด เพราะ!มีได้จากม้า Antlers";
	next;
	mes "[Cherokee]";
	mes "ฉันคิดว่าการมีเขาสัตว์ก็เป็นสิ่งที่ทำยามว่าเพื่อเก็บสะสม และถ้าต้องการ ก็ไปหา ^FF3355Evil Horn^000000 ให้ครบ 20 ชิ้น สามารถหาได้จากสัตว์ หากคุณต้องการ";
	next;
	mes "[Cherokee]";
	mes "ว่ายังไง...";
	mes "คุณมี ^FF3355Evil Horn^000000 หรือไม่?";
	mes "ถ้าหากคุณมี Evil Horns 20 ชิ้น  ฉันจะมอบ ^3355FFAntler^000000 สำหรับเป็นของสะสมเป็นการตอบแทน..?";
	next;
	switch(select("ตกลง -> จะแลก:หุบปากซะ -> ฉันไม่อยากฟัง")) {
	case 1:
		if (countitem(923) > 19) {
			delitem 923,20;  //Evil_Horn
			mes "[Cherokee]";
			mes "ยู้ฮู้ว...~! ท่านหา ^FF3355Evil Horn^000000 ได้รวดเร็วจริง ๆ ยอดเยี่ยม!!";
			mes "ขอบคุณ..! ฉันจะมอบ Antler ตามที่สัญญา!";
			getitem 2284,1; // Antler
			next;
			mes "[Cherokee]";
			mes "ขอบคุณที่ช่วยล่าเขาม้าให้..เป็นงัยสนุกไหม~!";
			close;
		}
		else {
			mes "[Cherokee]";
			mes "เออ...";
			mes "ฉันไม่สามารถแลกให้คุณได้หรอกคุณต้องมี ^FF3355 Evil Horns ^000000 ให้ครบ 20 ชิ้นก่อน";
			close;
		}
	case 2:
		mes "[Cherokee]";
		mes "ไม่เป็นไร... ท่านคงไม่ชอบล่าสัตว์แต่ถ้าหากคิดเปลี่ยนใจก็กลับมาหาข้าได้เลย...";
		close;
	}
}

//=======================================================================================================//
//			Bao Bao, Cresent Hairpin, Fashionbale Glasses, Heart Hairpin
//=======================================================================================================//
alberta,120,53,1	script	Stylish Merchant#new30	51,{
	if ((countitem(10007) > 0) && (countitem(968) > 49)) {
		mes "[Zic]";
		mes "ฉันรู้ ฉันรู้ ว่าคุณนั้นต้องการ Bao Bao แต่หากฉันไม่มีสมาธิฉันก็อาจทำมันไม่สำเร็จ ฉะนั้นรอก่อนน่ะ";
		next;
		mes "^3355FF*สมาธิ! สมาธิ!*";
		mes "*ฮา! อืม! ...!*";
		mes "*บูม บูม!*^000000";
		next;
		delitem 10007,1;  //Silk_Ribbon
		delitem 968,50;  //Voucher_Of_Orcish_Hero
		mes "[Zic]";
		mes "เสร็จสมบูรณ์!";
		mes "นี่งัยของที่ท่านต้องการ~!";
		mes "เนื้องานออกมาดี ยอดเยี่ยมเลย!!";
		getitem 5042,1; // Hair_Protector
		close;
	}
	else if((countitem(5041) > 0) && (countitem(999) > 9)) {
		mes "[Zic]";
		mes "ฉันเข้าใจแล้วว่าต้องการ Crescent Hairpin ฉันจะเริ่มทำให้เดียวนี้ แต่หากฉันไม่มีสมาธิฉันก็อาจทำมันไม่สำเร็จ ฉะนั้นรอก่อนน่ะ!";
		next;
		mes "^3355FF*สมาธิ! สมาธิ!*";
		mes "*ฮา! อืม! ย๊าก..!*";
		mes "*ตูม ตูม วิงค์..!*^000000";
		next;
		delitem 5041,1;  //Heart_Hair_Pin
		delitem 999,10;  //Steel
		mes "[Zic]";
		mes "เสร็จสมบูรณ์!";
		mes "มันช่างดีเหลือเชื่อ~!";
		mes "อ่ะฉันทำเสร็จแล้ว..";
		mes "ดูสิใส่ดูแล้วน่ารัก";
		mes "มากกว่าเดิมเยอะเลย, จริงป่ะ?";
		getitem 5048,1; // First_Moon_Hair_Pin
		close;
	}
	else if((countitem(2271) > 0) && (countitem(975) > 0)) {
		mes "[Zic]";
		mes "ว๊าว..ท่านหาของมาให้ครบด้วยแหะดูท่าอยากได้ Fashionable Glasses แต่โปรดรอข้าสักครู่ข้าจะเข้าไปในห้องทำหมวก";
		next;
		mes "^3355FF*โป๊ง! เป๊ง!*";
		mes "*กรึก! โป๊งโป๊ง! เป๊ง..!*";
		mes "*ปั๊ก..ซู่..!*^000000";
		next;
		delitem 2271,1;  //Jack_A_Dandy
		delitem 975,1;  //Scarlet_Dyestuffs
		mes "[Zic]";
		mes "เสร็จสมบูรณ์เรียบแล้วน่ะ!";
		mes "เชิญดูได้เต็มตาเลย ฉันคิดว่านี่เป็นแว่นที่ดูสวยมาก งานระดับ World Class.";
		getitem 5047,1; // Fashion_Sunglass
		close;
	}
	else if(countitem(7013) > 1199){
		mes "[Zic]";
		mes "ตกลง ตกลง คุณต้องการ Heart hairpin ฉันกำลังจะเริ่มทำให้เดียวนี้เลย แต่คุณคงรู้น่ะว่าหากฉันไม่มีสมาธิฉันก็ไม่สามารถทำให้คุณได้เลย ฉะนั้นโปรดรอสักครู่";
		next;
		mes "^3355FF*โป๊ง! เป๊ง!*";
		mes "*กรึก! โป๊งโป๊ง! เป๊ง..!*";
		mes "*ปั๊ก..ซู่..!*^000000";
		next;
		delitem 7013,1200;  //Coral_Reef
		mes "[Zic]";
		mes "เสร็จสมบูรณ์~!";
		mes "เสร็จสิ้น แหม่ ดูแล้วสวยงามจริง ๆ!";
		mes "แค่ฉันทำได้ก็มีความสุข และ";
		mes "ก็ทำให้คุณนั้นมีความสุข..~";
		getitem 5041,1; // Heart_Hair_Pin
		close;
	}
	else {
		mes "[Zic]";
		mes "ฮัลโหล.. สวัสดีจ๊ะ";
		mes "ข้าชอบที่นี่จัง...!";
		mes "ฉันชอบทะเล...!";
		next;
		mes "[Zic]";
		mes "...แต่?";
		mes "มีอะไรหรอ. วันหยุดนี้ท่านจะไปไหนหรอ..? แล้วจะเที่ยวที่ไหนหรือในวันหยุดนี้..";
		next;
		mes "[Zic]";
		mes "ฉันมีของดี ๆ จะมาแนะนำให้ท่าน เผื่อท่านสนใจบ้างมันคือหมวกแฟชั่นต่าง ๆ";
		next;
		mes "[Zic]";
		mes "รู้ไหมหมวกพวกนี้ที่ข้ามีนั้นข้าคัดสรรสิ่งของมาหมดแล้วเรื่องคุณภาพและความสวยงาม เชิญเลือกดูก่อนได้..";
		next;
		switch(select("Bao Bao:Cresent Hairpin:Fashionable Glasses:Heart Hairpin")) {
		case 1:
			mes "[Zic]";
			mes "หมวกชิ้นนี้...";
			mes "คุณต้องการให้ฉันทำ Bao Bao ไหม? ถ้าใช่คุณต้องหาของตามที่ข้าบอกต่อไปนี้...";
			next;
			mes "[Zic]";
			mes "สิ่งที่ต้องใช้ในการทำ...";
			mes "1 ^0000FFSilk Ribbon^000000";
			mes "50 ^0000FFHeroic Emlbem^000000";
			mes "ok คุณคงจำได้แล้วใช่ไหม?";
			close;
		case 2:
			mes "[Zic]";
			mes "หากต้องการ Crescent Hairpin ฉันจะบอกของที่จะต้องไปหา";
			next;
			mes "[Zic]";
			mes "นี่คือของที่ต้องไปหา";
			mes "1 ^0000FFHeart hair pin^000000";
			mes "10 ^0000FFSteel^000000";
			mes "...คุณพร้อมจะหาของหรือยัง?";
			close;
		case 3:
			mes "[Zic]";
			mes "เออ หากต้องการ Fashionable Glasses ฉันคิดว่าคุณต้องหาของที่ผมต้องการ";
			next;
			mes "[Zic]";
			mes "ฉันต้องการ...";
			mes "1 ^0000FFJack a Dandy^000000";
			mes "1 ^0000FFScarlet Dyestuffs^000000";
			mes "...ฉันรู้ว่าคุณคงเตรียมพร้อมแล้วละ?";
			close;
		case 4:
			mes "[Zic]";
			mes "คุณต้องการ heart hairpin! โอเค ฉันคิดว่าอาจจะเป็นงานยากของคุณแต่คุณคงจะพยายามทำแน่นอน";
			next;
			mes "[Zic]";
			mes "สิ่งที่ต้องหามีเพียงอย่างเดียวคือ...";
			mes "1200 ^0000FFCoral Reef^000000.";
			mes "...อืม คงพร้อมแล้วใช่ไหม?";
			close;
		}
	}
}


alberta_in,23,104,4	script	Grandpa Turtle#tur	120,{
	mes "[Grandpa Turtle]";
	mes "โอ..ก๊อด...";
	mes "ว่างัย..สหายตัวน้อย";
	next;
	mes "[Grandpa Turtle]";
	mes "ที่เมือง Alberta ไม่มีร้านดื่มดี ๆเลย แต่ทำไมข้าถึงดื่มมาก จนเวียนหัวไปหมดแล้ว";
	next;
	switch(select("ช่วยบอกทีว่า เกาะเต๋าคืออะไร:ฉันสามารถจะไปได้อย่างไร?:หยุดพูด")) {
	case 1:
		mes "[Grandpa Turtle]";
		mes "อื๋ม หือ....?";
		mes "Turtle Island?";
		next;
		mes "[Grandpa Turtle]";
		mes "มีคนแถวนี้พูดเรื่องนี้กันเป็นประจำ บ้างก็เคยไปมากันแล้ว บางคนบอกว่าเป็นเกาะแห่งสมบัติ";
		next;
		mes "[Grandpa Turtle]";
		mes "แต่คนบางกลุ่มเขาก็บอกว่า เกาะเต๋าปริศนานั้นมีโสมวิเศษอยู่ที่นั่น แต่มันก็แค่เสียงปากของชาวเมือง";
		next;
		mes "[Grandpa Turtle]";
		mes "แต่ฉันก็ยังเชื่อน่ะว่า  Turtle Island มีที่ตั้งอยู่จริง...และอาจมีสิ่งมหัศจรรย์";
		next;
		mes "[Grandpa Turtle]";
		mes "แต่ผู้สามารถเข้าไปในนั้นได้ คงต้องมีความสามารถและเป็นที่มีความมุ่งมั่นอย่างแท้จริงที่จะค้นหาเกาะมหัศจรรย์";
		next;
		switch(select("^3333FF*จริงหรือ จริงหรือ*^000000:ขอข้อมูลเพิ่มเติมที")) {
		case 1:
			mes "[Grandpa Turtle]";
			mes "ฮิ ฮิ ฮ่า..!";
			if (Sex)
				mes "ข้านั้นชอบความติ๊ง ต๋อง ๆ ของเจ้าจริง ๆ เลยน่ะ ฮ่า ฮ่า ฮา...ฮิ ฮิ ฮิ";
			else
				mes "เธอนี่เวลาทำหน้าจริงจังนี่ ดูน่ารัก น่าเอ็นดู  ดีน่ะ...";
			next;
			mes "[Grandpa Turtle]";
			mes "ฟังฉันล่ะ...";
			mes "เมื่อนานมาแล้วนั้น บุคคลหนึ่งเป็นผู้ค้นหาเกาะเต๋าเจอผู้แรก เขาคือ ^3355FFJornadan Niliria^000000 ฉันจะเล่าเรื่องของเขา";
			next;
			mes "[Grandpa Turtle]";
			mes "^3355FFJornadan Niliria^000000 เขาและเพื่อน ๆ นับสิบ ๆ คนได้ค้นพบเกาะเต๋าจริง ๆ";
			next;
			mes "[Grandpa Turtle]";
			mes "^3355FFJornadan Niliria^000000 เขาได้เรียนรู้จักความแข็งแกร่งและมีความเชื่อมันในตนเองเขาและลูกเรือสืบหาข้อมูลเกี่ยวกับ เกาะเต๋า";
			next;
			mes "[Grandpa Turtle]";
			mes "และหลังจากข้อมูลที่หาได้ทั้งหมดนั้นการเดินทางจึงได้เริ่มต้นขึ้นเขานั้นมีความเชื่อที่จะได้พบเกาะในตำนานอีกสถานที่หนึ่ง เขาชื่อว่าเกาะนั้นมีอยู่จริง";
			next;
			mes "[Grandpa Turtle]";
			mes "เวลาผ่านไปแสนยาวนาน ความลำบากที่พบก็มากมาย แต่ความของ Niliria's ก็ทำได้สำเร็จพวกเขาค้นพบ Turtle Island";
			next;
			mes "[Grandpa Turtle]";
			mes "อย่างไรก็ตามแต่มันมีความซับซ้อนหลังจากข้าได้ยินเสียง Niliria's ลูกเรือได้กลับบ้านด้วยเหตุผลบางประการ";
			next;
			mes "[Grandpa Turtle]";
			mes "หากเจ้าต้องการข้อมูลเพิ่มเติมทำไมเจ้าไม่ไปทางตะวันออกของ Alberta? ที่นั่นมีนักวิชาการที่มีความรู้เกี่ยวกับเรื่องนี้";
			next;
			mes "[Grandpa Turtle]";
			mes "เขาอาจจะสามารถบอกท่านเพิ่มเติมได้เกี่ยวกับเรื่องเล่าของ Turtle Island";
			next;
			mes "[Grandpa Turtle]";
			mes "คุณควรจะไปหาเขา Journal ข้าคิดว่ามันเป็นสิ่งที่น่าสนใจ";
			next;
			mes "[Grandpa Turtle]";
			mes "อ่า";
			mes "ไฟของคนรุ่นใหม่นี่ช่างร้อนแรงจริง ๆ ";
			close;
		}
		mes "[Grandpa Turtle]";
		mes "อืมท์.....!";
		next;
		mes "[Grandpa Turtle]";
		mes "ข้ารู้ไม่มากหรอก..แต่ข้านั้นจะพูดกับคนที่มีความโลภ เจ้ากลับไปซะ";
		close;
	case 2:
		if ((MISC_QUEST & 65536) || TURTLE) {
			if(MISC_QUEST & 65536) set TURTLE,0; //now clear the var
			mes "[Grandpa Turtle]";
			mes "เอ๋...";
			mes "ถ้าเจ้าไปที่ท่าเรือของ Alberta เจ้าควรจะไปหาเค้า ผู้เฒ่าแจวเรือ";
			next;
			mes "[Grandpa Turtle]";
			mes "ชื่อของเขา ^3355FFGotanblue^000000 ข้าเชื่อว่าเขาน่าจะรู้วิธีไป Turtle Island ขอให้โชคดี..";
			close;
		}
		mes "[Grandpa Turtle]";
		mes "Turtle Island...?";
		next;
		mes "[Grandpa Turtle]";
		mes "เป็นสถานที่หายาก และมีอันตรายเจ้าแน่ใจแล้วหรือว่าต้องการไป ข้าจะให้คำแนะนำบางอย่าง แต่ข้ารู้สึกเวียนหัวหลังจากที่ดื่มมาก";
		next;
		mes "[Grandpa Turtle]";
		mes "ฮิ ฮิ ฮิ...";
		mes "ทำไมเจ้าไม่ลองไปคุยกับกะลาสีชื่อ ^3355FFGotanblue^000000 เจ้าสามารถหาเค้าได้ที่ท่าเรือ Alberta";
		next;
		mes "[Grandpa Turtle]";
		mes "บอกเขาว่าข้าส่งเจ้าไปและเขาจะบอกเพิ่มเติมเกี่ยวกับ Turtle Island เขาอาจจะรู้แม้กระทั่งวิธีไป";
		next;
		mes "[Grandpa Turtle]";
		mes "หลังจากนี้...";
		mes "ขอให้โชคดี~";
		set TURTLE,1;
		close;
	case 3:
		mes "[Grandpa Turtle]";
		mes "โอ้โห..ข้าไม่พูดก็ได้";
		mes "เจ้านี่มันไม่ชอบการผจญภัยซะจริง";
		mes "ฉันละไม่เข้าใจเลย..";
		mes "พวกที่ไม่ชอบเอาความสามารถออกมาใช้";
		close;
	}
}

alberta,245,121,4	script	Sailor#tur	709,{
	if (TURTLE || (MISC_QUEST & 65536)) {
		mes "[Gotanblue]";
		mes "เฮ้.....ว่างัยหรือ";
		mes "ดวงตาของท่านดูแล้วช่างมีความมุ่งมั่นเหลือเกิน..";
		mes "ฉันสามารถคุณเกี่ยวกับ Turtle Island ฉันคิดว่าคุณคงได้ข้อมูลมาจาก Grandpa";
		next;
		switch(select("ช่วยเล่าเรื่องตำนาน Turtle Island ที?:ทำอย่างไรข้าถึงจะได้ไปที่แห่งนี้?:หยุดการสนทนา")) {
		case 1:
			mes "[Gotanblue]";
			mes "Turtle Island...?";
			mes "แต่, สิ่งแรกที่คุณควรจะรู้เกี่ยวกับเกาะ Turtle Island คุณควรมีเพื่อนที่จะไปด้วย คุณคงจะเคยได้ยินตำนาน^3355FFJornadan Niliria^000000'ท่านผู้ยิ่งใหญ่";
			next;
			mes "[Gotanblue]";
			mes "พวกเขาได้สร้างตำนานให้กับเกาะแห่งนี้... เขาสามารถค้นพบเกาะที่หายสาบสูญมานานได้สำเร็จ";
			next;
			mes "[Gotanblue]";
			mes "และ Jornadan Niliria เขาได้พบกลับถ้ำสมบัติมีมูลค่ามหาศาลทำให้เขามีชื่อเสียงในหมู่นักล่าด้วยกัน";
			next;
			mes "[Gotanblue]";
			mes "และฉันก็สามารถนำทางให้คุณสามารถไปอย่างเขาได้ แต่เพื่อความปลอดภัยของท่านควรจะมีทีมนักล่าไว้ช่วยเหลือท่าน";
			next;
			mes "[Gotanblue]";
			mes "เพราะเกาะ Turtle Island แห่งนี้มีผู้เฝ้าเกาะแห่งนี้เป็นเหมือนเจ้าที่..อย่างไงอย่างงั้น";
			next;
			mes "[Gotanblue]";
			mes "หากท่านต้องการไป ท่านก็ต้องระวังตัวท่านให้มาก ๆ เพราะประมาทเพียงนิดเดียวท่านอาจทิ้งชีวิตที่ เกาะแห่งนี้ได้";
			next;
			mes "[Gotanblue]";
			mes "เพราะเกาะแห่งนี้นอกจากท่านผู้นั้นแล้ว ก็ไม่เคยมีผู้ใดออกมาจากเกาะนั้นได้อีกเลยไม่ว่าจะมีผู้เก่งกล้านับร้อยพันก็ไม่สามารถนำชีวิตตัวเองออกมาได้";
			next;
			mes "[Gotanblue]";
			mes "คงถึงเวลาแล้วละเรื่องเล่าที่ข้าพูดมานั้น อยู่ที่ท่านจะสร้างตำนานบทใหม่ได้หรอไม่";
			close;
		case 2:
			mes "[Gotanblue]";
			mes "เรื่องราวของ Turtle Island คุณคงฟังมาเยอะแล้วละฉันว่าคุณคงพร้อมจะเดินทางซักที";
			next;
			mes "[Gotanblue]";
			mes "ฉันจะแนะนำเอง เพียงท่านต้องมีเงินค่าเดินทาง 10,000 zeny และฉันจะพาไปไปส่งคุณอย่างปลอดภัย";
			next;
			if (select("Turtle island -> 10000 zeny:ยกเลิก") == 1) {
				if (Zeny > 9999) {
					mes "[Gotanblue]";
					mes "เข้าใจแล้ว..!!";
					mes "ข้าต้องจ่ายเงินค่าเดินทาง 10000 ในการนั่งเรือพร้อมสเบียง";
					next;
					mes "^3355FF*ออกเดินทางได้ สู่เกาะเต๋า*^000000";
					set zeny,zeny-10000;
					warp "tur_dun01",157,39;
					close;
				}
				mes "[Gotanblue]";
				mes "อืมท์.....";
				mes "ขอโทษด้วย.., เพราะท่านไม่มีเงินพอในการใช้เดินทางหากท่านขาดเงินในการเดินทางเราก็จะไม่มีสเบียงพอที่จะไป Turtle island...";
				close;
			}
			mes "[Gotanblue]";
			mes "ข้าเข้าใจท่านดี..";
			mes "แต่.. หากว่าท่านพร้อมที่จะออกเดินทางไป โปรดมารอข้าที่นี่";
			close;
		case 3:
			mes "[Gotanblue]";
			mes "เฮ้ย.....";
			next;
			mes "[Gotanblue]";
			mes "หากท่านต้องการที่จะเริ่มผจญภัยแล้ว ก็กลับมาหาข้าได้ทุกเวลา";
			close;
		}
	}
	mes "[Gotanblue]";
	mes "อ่ะฮ่า.....!";
	mes "เจ้าลองมองดูทะเลนี่สิ..! มันคงอยู่ไกลในสายตาของท่านในตอนนี้.. ข้าคงจะบอกความลับของท้องทะเลที่ท่านจะไปผจญภัยมิได้หรอก";
	next;
	mes "ท่านลองไปคุยกับ Grandpa ดู";
	close;
}

tur_dun01,165,29,4	script	Sailor#tur2	709,{
	mes "[Gotanblue]";
	mes "คุณต้องการ";
	mes "จะกลับมา";
	mes "เมือง Alberta?";
	next;
	if (select("กลับ Alberta:จบการสนทนา") == 1) {
		mes "[Gotanblue]";
		mes "เย่ เย่....";
		mes "หากคุณเหนื่อยกับการผจญภัยแล้วอยากกลับไปพัก เราจะพากลับเมืองด้วยเรือ ";
		next;
		mes "^3355FF* ออกเดินทางได้แล้ว *^000000";
		warp "alberta",241,115;
	}
	close;
}