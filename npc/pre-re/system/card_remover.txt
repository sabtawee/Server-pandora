//===== rAthena Script =======================================
//= Card Removal NPC
//===== By: ==================================================
//= TyrNemesis^
//===== Current Version: =====================================
//= 1.2a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Removes cards from equipped items.
//===== Additional Comments: =================================
//= 1.0 First version. [TyrNemesis^]
//= 1.2 Optimized and fixed getequipname menu. [Kisuka]
//= 1.2a Added 'disable_items' command. [Euphy]
//============================================================

izlude,117,91,4	script	Wise Old Woman#eAcustom	78,{

	set .zenycost,5000000;    // base cost of the card remover services (in Zeny)
	set .percardcost,5000000;  // cost per card of the card remover services (in Zeny)
	set .faildestroy,0;      // should the card remover have a chance of failure that destroys items? (1=yes, 0=no)

	disable_items;
	mes "[Wise Old Woman]";
	mes "สวัสดีวันเด็ก ฉันมีอำนาจในการถอดการ์ดที่คุณประกอบเข้ากับอุปกรณ์ของคุณ ความคิดนี้ทำให้คุณพอใจไหม?";
	next;
	switch(select("ใช่.:คิดค่าอะไรบ้าง?:ไม่เป็นไรขอบคุณ.")) {
	case 1:
		mes "[Wise Old Woman]";
		mes "ดีมาก. ฉันจะตรวจสอบรายการใดสำหรับคุณ?";
		next;

		setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
		for( set .@i,1; .@i <= 10; set .@i,.@i+1 ) {
			if( getequipisequiped(.@indices[.@i]) )
				set .@menu$, .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
			set .@menu$, .@menu$ + ":";
		}
		set .@part, .@indices[ select(.@menu$) ];
		if(!getequipisequiped(.@part)) {
			mes "[Wise Old Woman]";
			mes "หนุ่มน้อย... คุณไม่ได้สวมอะไรที่นั่นที่ฉันสามารถถอดการ์ดออกจาก.";
			close;
		}
		if(getequipcardcnt(.@part) == 0) {
			mes "[Wise Old Woman]";
			mes "หนุ่มน้อย... ไม่มีการ์ดประกอบในรายการนี้ ฉันไม่สามารถทำอะไรกับมันได้ฉันกลัว.";
			close;
		}
		set .@cardcount,getequipcardcnt(.@part);
		
		if (!checkweight(1202,(.@cardcount+1))) { 
			mes "^3355FFJust a minute!";
			mes "ฉันไม่สามารถเสนอใดๆ ของฉัน";
			mes "บริการถึงคุณเพราะ";
			mes "คุณแบกมากเกินไป";
			mes "สิ่งของ. ใส่รายการพิเศษของคุณใน";
			mes "Kafra Storage แล้วมาใหม่~";
			close;
		}
		mes "[Wise Old Woman]";
		mes "รายการนี้มี " + .@cardcount + " การ์ดที่ประกอบอยู่บนนั้น เพื่อร่ายมนตร์ของข้า ข้าจะต้อง " + (.zenycost+(.@cardcount * .percardcost)) + " zeny, a ^0000FFStar Crumb^000000, and a ^0000FFYellow Gemstone^000000.";
		next;
		if(select("ดีมาก. ทำมัน.:ช่างเถอะ.") == 2) {
			mes "[Wise Old Woman]";
			mes "ดีมาก. กลับมาทันทีหากคุณต้องการใช้บริการของฉัน.";
			close;
		}
		if((zeny < (.zenycost+(.@cardcount * .percardcost))) || (countitem(1000) < 1) || (countitem(715) < 1)) {
			mes "[Wise Old Woman]";
			mes "คุณไม่มีไอเท็มทั้งหมดที่ฉันต้องการเพื่อใช้เวทย์มนตร์ของฉัน ที่รัก มาอีกครั้งเมื่อคุณทำ.";
			close;
		}
		mes "[Wise Old Woman]";
		mes "ก่อนที่ฉันจะเริ่ม ฉันต้องเตือนคุณ - ฉันอาจล้มเหลว ถ้าฉันทำ ฉันอาจทำลายการ์ด ไอเท็ม หรือทั้งสองอย่าง ฉันไม่คืนเงินให้ ที่บอกว่าสำคัญกว่าสำหรับคุณ: การ์ดหรือไอเท็ม?";
		next;
		switch(select("ฉันเปลี่ยนใจเกี่ยวกับเรื่องนี้.:ฉันเลือก item.:ฉันเลือก cards.")) {
		case 1:
			mes "[Wise Old Woman]";
			mes "ดีมาก. กลับมาทันทีหากคุณต้องการใช้บริการของฉัน.";
			close;
		case 2:
			set .@failtype,1;
			break;
		case 3:
			set .@failtype,2;
		}
		mes "[Wise Old Woman]";
		mes "ดีมาก. ฉันจะเริ่ม.";
		set Zeny, Zeny - (.zenycost+(.@cardcount * .percardcost));
		delitem 1000,1; //Star_Crumb
		delitem 715,1; //Yellow_Gemstone
		
		// Replace the constants in the next 3 lines with failure chance values defined in refine_db.txt
		// First value = Total failure chance (item and cards destroyed)
		// Second value = Partial failure chance (one or the other is destroyed, player decides which one is safe)
		// Third value = Harmless failure chance (all that's lost is your investment)

		set .@failchance,rand(100);
		if (.faildestroy==1) {
			if(.@failchance < 2) {
				next;
				failedremovecards .@part,0;
				mes "[Wise Old Woman]";
				mes "กระบวนการนี้เป็นความล้มเหลวทั้งหมด ฉันเกรงว่าไอเท็มและการ์ดจะถูกทำลาย.";
				close;
			}

			if(.@failchance < 8) {
				if (.@failtype == 1) {
					next;
					failedremovecards .@part,1;
					mes "[Wise Old Woman]";
					mes "While I have managed to remove the cards from the item, they were destroyed in the process. The item, however, is okay.";
					close;
				}

				if (.@failtype == 2) {
					next;
					failedremovecards .@part,2;
					mes "[Wise Old Woman]";
					mes "Most unfortunate. I succeeded at removing the cards, but the item itself was destroyed in the process.";
					close;
				}
			}
		}

		if(.@failchance < 10) {
			next;
			failedremovecards .@part,3;
			mes "[Wise Old Woman]";
			mes "I have failed to remove the cards. Luckily, however, both the item and the cards are still okay.";
			close;
		}
		next;
		successremovecards .@part;
		mes "[Wise Old Woman]";
		mes "The process was a success. Here are your cards and your item. Farewell.";
		close;
	case 2:
		mes "[Wise Old Woman]";
		mes "I charge a flat fee of "+callfunc("F_InsertComma",.zenycost)+" zeny, plus "+callfunc("F_InsertComma",.percardcost)+" zeny for each card I remove from the item. In addition, I need a star crumb and a yellow gemstone to work my magic.";
		close;
	case 3:
		mes "[Wise Old Woman]";
		mes "ดีมาก. กลับมาทันทีหากคุณต้องการใช้บริการของฉัน.";
		close;
	}
OnInit:
	waitingroom "  ถอดการ์ด ",0;
	end;

}
